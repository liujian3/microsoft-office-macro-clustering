Attribute VB_Name = "Basic"
Option Explicit

Const NazevDokumentu = "P|fffd|ed|fffd|vac|fffd| protokol"
Dim Autor As String
Dim Dekret As String

Sub NahradaKlicu()
   Dim Radek As String
   Dim Kanal As Integer
   Dim Klic As String
   Dim Hodnota As String
   Dim Smlouva As String
   
   On Error GoTo Chyba
   frmHlaseni.Repaint
   Application.ScreenUpdating = False
   Kanal = FreeFile
   Open ThisDocument.Path & "\ID.cfg" For Input As Kanal
   Do While Not EOF(Kanal)
      Line Input #Kanal, Radek
      If Radek <> "" Then
         Klic = DejKlic(Radek)
         Hodnota = DejHodnotuKlice(Radek)
         Nahrad Klic, Hodnota
         Select Case Klic
         Case "<<rok>>"
            Nahrad "<<rok-1>>", Hodnota - 1
            Nahrad "<<rok+1>>", Hodnota + 1
         Case "<<@jmeno>>": Autor = Hodnota
         Case "<<@dekret>>": Dekret = Hodnota
         End Select
      End If
   Loop
   Nahrad "<<dnes>>", Format(Date, "dd. mm. yyyy")
   PristeNenahrazovat
Chyba:
   Close Kanal
   Unload frmHlaseni
   Application.ScreenUpdating = True
End Sub

Function DejKlic(Text As String) As String
   Dim Oddelovac As Integer
   DejKlic = ""
   Oddelovac = InStr(1, Text, "=")
   DejKlic = Trim(Left(Text, Oddelovac - 1))
End Function

Function DejHodnotuKlice(Text As String) As String
   Dim Oddelovac As Integer
   DejHodnotuKlice = ""
   Oddelovac = InStr(1, Text, "=")
   DejHodnotuKlice = Trim(Right(Text, Len(Text) - Oddelovac))
End Function

Sub Nahrad(ByVal Co As String, ByVal Cim As String)
   With ActiveDocument.Content.Find
      .Replacement.ClearFormatting
      .Text = Co
      .Replacement.Text = Cim
      .Forward = True
      .Wrap = wdFindContinue
      .Format = False
      .MatchCase = False
      .MatchWholeWord = False
      .MatchWildcards = False
      .MatchSoundsLike = False
      .MatchAllWordForms = False
      .Execute Replace:=wdReplaceAll
   End With
End Sub

Sub PristeNenahrazovat()
   On Error Resume Next
   ActiveDocument.Variables("Nahrazeno").Value = "true"
   If Err <> 0 Then
      ActiveDocument.Variables.Add Name:="Nahrazeno", Value:="true"
   End If
End Sub
Attribute VB_Name = "ThisDocument"
Attribute VB_Base = "1Normal.ThisDocument"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = True
Attribute VB_Customizable = True
Option Explicit

Private Sub Document_Open()
   Dim Nahrazeno As String
   On Error Resume Next
   Nahrazeno = ActiveDocument.Variables("Nahrazeno").Value
   If Err <> 0 Or Nahrazeno <> "true" Then
      frmHlaseni.Show
   End If
   CommandBars("Control Toolbox").Visible = False
End Sub

Attribute VB_Name = "frmHlaseni"
Attribute VB_Base = "0{33685344-F4DE-4724-B216-DE98550B1CCE}{772CC728-DB29-4590-A97A-6572CD27CFDA}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = False

Option Explicit

Private Sub UserForm_Activate()
   NahradaKlicu
End Sub



INQUEST-PP=macro
