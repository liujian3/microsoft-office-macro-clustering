Attribute VB_Name = "Common"
' WORD 97 MINI-WIZARD  -  Common VBA Module
' --------------------------------------------------------------------------------------
Option Explicit

' --------------------------------------------------------------------------------------
' CONSTANT DECLARATIONS

' Error messages
Public Const strERR_BAD_DOC         As String = "Der Assistent kann nicht im aktiven Dokument verwendet werden. Um den Assistenten zu verwenden, w|fffd|hlen Sie ""Neu"" im Men|fffd| ""Datei"" und w|fffd|hlen Sie den "
Public Const strERR_BAD_DOC2        As String = " aus."

' --------------------------------------------------------------------------------------
' ************     DO NOT LOCALIZE ANY CODE BEYOND THIS POINT  *************************
' --------------------------------------------------------------------------------------

' PUBLIC DECLARATIONS
Public fBalloonChoice           As Long
Public fAssistantAvailable      As Boolean
Public objHelpTipBalloon        As Balloon

Public Sub StartWizard()
        
    InitWizardStrings
    InitDoc
    System.Cursor = wdCursorNormal
    Application.ScreenRefresh
    InitiateWizard

End Sub ' StartWizard

Private Sub InitDoc()
    Dim fBadDoc         As Integer
    Dim objActiveRange  As Object
    
    On Error GoTo FatalError

    ' Check if doc.path errors if path not present.
    fBadDoc = False
    If (Len(ActiveDocument.Path) <> 0) Then
        'doc has been saved before
        fBadDoc = True
    End If
    
    If (fBadDoc) Then
        DisplayErrorMsg strERR_BAD_DOC & strWIZ_NAME & strERR_BAD_DOC2
        ActiveDocument.Close wdDoNotSaveChanges
        End ' Terminate wizard
    End If
    
    WordBasic.FileSummaryInfo Title:=""     'Need to use WB to zero this
    Exit Sub

FatalError:
    ReportError Err

End Sub  ' InitDoc


Public Sub InitiateWizard()
    Dim i                   As Integer
    Dim objInitialBalloon   As Object

    On Error GoTo ErrorInitBalloon
    
    ' Create new Assistant Balloon
    Set objInitialBalloon = Assistant.NewBalloon
    With objInitialBalloon
        .Heading = strBALLOON_HEADING
        .Button = msoButtonSetCancel
        .BalloonType = msoBalloonTypeButtons
    End With

    ' Define Balloon Text
    With objInitialBalloon.Labels
        .Count = iBALLOON_OPTIONS
        For i = 1 To iBALLOON_OPTIONS
            .Item(i).Text = strInitialBalloon(i - 1)
        Next i
    End With

    ' Show Balloon and get user's choice
    fBalloonChoice = objInitialBalloon.Show
    fAssistantAvailable = True
    
    ' Service choice
    On Error GoTo WizardError
    fnInitialBalloon (fBalloonChoice)
    Exit Sub
    
ErrorInitBalloon:
    ' Use Dialog Form
    fAssistantAvailable = False
     
    On Error GoTo WizardError
    
    ' Initialize form labels
    formOptions.Caption = strWIZ_NAME
    formOptions.lblPrompt.Caption = strBALLOON_HEADING
    formOptions.optOne.Caption = strInitialBalloon(0)
    formOptions.optTwo.Caption = strInitialBalloon(1)

    ' Set localizable accelerator keys
    formOptions.optOne.Accelerator = strOPTION_ONE_ACCELERATOR
    formOptions.optTwo.Accelerator = strOPTION_TWO_ACCELERATOR
     
    ' Show Form and get user's choice
    fBalloonChoice = -1
    formOptions.Show
    
    ' Service choice
    On Error GoTo WizardError
    fnInitialBalloon (fBalloonChoice)
    Exit Sub
    
WizardError:
    ReportError Err
    
End Sub ' InitiateWizard


Public Sub ReportError(ErrorCode As ErrObject)
    Dim lErrorCode As Long
    Dim strErrorMsg As String
    
    '
    lErrorCode = ErrorCode.Number
    strErrorMsg = ErrorCode.Description
    
    ' Display error
    If lErrorCode Then DisplayErrorMsg strErrorMsg
    
    ' Always close the doc.
    ActiveDocument.Close wdDoNotSaveChanges
    
    ' Terminate Wizard
    End

End Sub ' ReportError


Public Sub DisplayErrorMsg(strErrMsg As String)
    
    MsgBox strErrMsg, vbExclamation + vbOKOnly + vbApplicationModal, strWIZ_NAME
    Err.Clear
    
End Sub


Public Sub ShowAssistantTip()

    On Error GoTo ErrorInitBalloon
    
    ' Create new Assistant Balloon
    Set objHelpTipBalloon = Assistant.NewBalloon
    With objHelpTipBalloon
        .Heading = strWIZ_NAME
        .Mode = msoModeModeless
        .Button = msoButtonSetNone
        .Text = strHelpTip(fBalloonChoice - 1)
    End With

    ' Show Balloon
    objHelpTipBalloon.Show
    Exit Sub
    
ErrorInitBalloon:
End Sub

' End Common Module
Attribute VB_Name = "Letter"
' --------------------------------------------------------------------------------------
' WORD 97 MINI-WIZARD
' Letter Wizard Specific Code
' --------------------------------------------------------------------------------------

Option Explicit

' --------------------------------------------------------------------------------------
' DECLARATIONS
' --------------------------------------------------------------------------------------

Public Const strWIZ_NAME = "Brief-Assistent"
Public Const iBALLOON_OPTIONS = 2 ' Number of items in InitialBalloon (First=1)
Public Const strBALLOON_HEADING = "Willkommen beim Brief-Assistenten."
Public Const strOPTION_ONE_ACCELERATOR = "E"   'This is the form accelerator for strInitialBalloon(0)
Public Const strOPTION_TWO_ACCELERATOR = "S"   'This is the form accelerator for strInitialBalloon(1)
Public strInitialBalloon(iBALLOON_OPTIONS) As String
Public strHelpTip(iBALLOON_OPTIONS) As String


Public Sub InitWizardStrings()
  
' NOTE: change constant  iBALLOON_OPTIONS if number of items in list changes
    ' Balloon / Form menu options
    strInitialBalloon(0) = "Einen Brief an einen Empf|fffd|nger schreiben."
    strInitialBalloon(1) = "Einen Serienbrief erstellen."
    
    ' Assistant help messages
    strHelpTip(0) = "n/a"
    strHelpTip(1) = "Sie k|fffd|nnen die Serienbrief-Funktion in Word verwenden, um Briefe an mehrere Empf|fffd|nger zu versenden. Die Anleitungen im oberen Teil des ""Seriendruck-Managers"" helfen Ihnem beim Erstellen eines Serienbriefes."

End Sub  ' InitWizardStrings


Public Function fnInitialBalloon(iBtn As Integer)
    
    Select Case iBtn
        Case Is < 1 'Exit
            ActiveDocument.Close wdDoNotSaveChanges
        Case 1 ' Run the Letter Wizard
            ' This dialog has its own assistant tip
            ActiveDocument.RunLetterWizard
        Case 2 ' Start a Mail Merge to letters
            ActiveDocument.MailMerge.MainDocumentType = wdFormLetters
            If fAssistantAvailable Then ShowAssistantTip
            Application.Dialogs(wdDialogMailMergeHelper).Show
            If fAssistantAvailable Then objHelpTipBalloon.Close
    End Select
    Exit Function
    
End Function ' fnInitialBalloon

Public Sub SetDate()
    Const str_A4_DATE = "t. MMMM jjjj"
    Dim CurrentDate As Field
    Dim fFoundDate As Boolean
    Dim iFieldCount, iDateCounter As Long

    If fnQueryA4 Then
        fFoundDate = False
        iFieldCount = ActiveDocument.Fields.Count
        iDateCounter = 1
        While (Not fFoundDate) And (iDateCounter <= iFieldCount)
            Set CurrentDate = ActiveDocument.Fields(iDateCounter)
            If CurrentDate.Type = wdFieldDate Then fFoundDate = True
            iDateCounter = iDateCounter + 1
        Wend
        If fFoundDate Then
            CurrentDate.Code.Text = "DATE \@ " + Chr$(34) + str_A4_DATE + Chr$(34)
            CurrentDate.Update
            ActiveDocument.Saved = True
        End If
    End If
End Sub ' SetDate
Function fnQueryA4() As Boolean

    Const strA4_SETTING As String = "A4"
    Const strREG_PAGE_SIZE As String = "PageSize"
    Const strREG_SETTINGS_BASE_KEY As String = "HKEY_CURRENT_USER\Software\Microsoft\Office\8.0\Word\Wizards\"
    Dim strProperty As String

    On Error GoTo ClearError
    
    fnQueryA4 = False 'Assume non-A4 date by default
    strProperty = System.PrivateProfileString("", strREG_SETTINGS_BASE_KEY, strREG_PAGE_SIZE)
    If strProperty = strA4_SETTING Then fnQueryA4 = True
    Exit Function

ClearError:
    Err.Clear

End Function ' fnQueryA4

Public Sub NoMacro()
End Sub

' End Letter Wizard Module
Attribute VB_Name = "ThisDocument"
Attribute VB_Base = "0{00020906-0000-0000-C000-000000000046}"
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Private Sub Document_New()
    ActiveDocument.Fields.Update
    SetDate
    StartWizard
End Sub

Attribute VB_Name = "formOptions"
Attribute VB_Base = "0{7FDF0462-52C3-11D0-B5E8-00AA00BBB76C}{7FDF0448-52C3-11D0-B5E8-00AA00BBB76C}"
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = False






' Word Wizard
Option Explicit

Private Sub cmdCancel_Click()
    ' Close Form
    formOptions.Hide
End Sub

Private Sub cmdOK_Click()
    ' Save global choice
    If optOne.Value Then
        fBalloonChoice = 1
    Else
        fBalloonChoice = 2
    End If
    
    ' Close Form
    formOptions.Hide
End Sub

' InQuest injected base64 decoded content
' jZ 1

INQUEST-PP=macro
