Attribute VB_Name = "Module1"
Function Drawdown(rDate As Range, rPrice As Range) As Variant
    ' shg 2013-0202

    ' Returns an array of six values:
    
    ' a(1,1) = Date of peak value for maximum drawdown
    ' a(1,2) = Price on that date
    ' a(1,3) = unused (blank)
    
    ' a(2,1) = Date of max drawdown
    ' a(2,2) = Price on that date
    ' a(2,3) = Drawdown
    
    ' http://en.wikipedia.org/wiki/Drawdown_(economics)

    Dim i           As Long
    Dim dPeak       As Double
    Dim dPeakSav    As Double
    Dim tPeak       As Date
    Dim tPeakSav    As Date
    Dim dDD         As Double
    Dim dDDMax      As Double
    Dim tDDMax      As Date

    ReDim DDt(1 To rPrice.Cells.Count)

    For i = 1 To rPrice.Cells.Count
        If Not IsEmpty(rPrice(i)) Then
            If rPrice(i).Value2 > dPeak Then
                dPeak = rPrice(i).Value2
                tPeak = rDate(i).Value
            End If

            dDD = 1 - rPrice(i).Value2 / dPeak
            If dDD > dDDMax Then
                dDDMax = dDD
                tDDMax = rDate(i).Value
                dPeakSav = dPeak
                tPeakSav = tPeak
            End If
        End If
    Next i

    Drawdown = Array(Array(tPeakSav, dPeakSav, vbNullString), _
                     Array(tDDMax, dPeakSav * (1 - dDDMax), dDDMax))
End Function



Attribute VB_Name = "Sheet4"
Attribute VB_Base = "0{00020820-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Attribute VB_Name = "ThisWorkbook"
Attribute VB_Base = "0{00020819-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True


INQUEST-PP=macro
