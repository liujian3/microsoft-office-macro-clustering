Attribute VB_Name = "Module1"
Function VYPISVDOLARECH(bunka) As Variant

    Dim Dolary As String
    Dim Centy As String
    Dim DelkaTextu As Integer
    Dim Pom As String
    Dim Pos As Integer
    Dim iStovky As Integer
    Dim iDesitky As Integer
    Dim iJednotky As Integer
    Dim Jednotky(2 To 5) As String
    Dim bNalezeno As Boolean
    Dim JednaAzDevet As Variant
    Dim DesetAzDevatenact As Variant
    Dim DvacetAzDevadesat As Variant
    Dim PriznakZapornehoCisla As Boolean

'   Nen|fffd| to |fffd||fffd|slo?
    If Not IsNumeric(bunka) Then
        VYPISVDOLARECH = CVErr(xlErrValue)
        Exit Function
    End If

'   Je z|fffd|porn|fffd|?
    If bunka < 0 Then
        PriznakZapornehoCisla = True
        bunka = Abs(bunka)
    End If
    
    Dolary = Format(bunka, "###0.00")
    DelkaTextu = Len(Dolary) - 3

'   Je p|fffd||fffd|li|fffd| velk|fffd|?
    If DelkaTextu > 15 Then
        VYPISVDOLARECH = CVErr(xlErrNum)
        Exit Function
    End If

'   V|fffd|po|fffd|et cent|fffd|
    Centy = Right(Dolary, 2) & "/100 Dollars"
    If bunka < 1 Then
        VYPISVDOLARECH = Centy
        Exit Function
    End If

    Dolary = Left(Dolary, DelkaTextu)

    JednaAzDevet = Array("", "One", "Two", "Three", "Four", "Five", "Six", "Seven", "Eight", "Nine")
    DesetAzDevatenact = Array("Ten", "Eleven", "Twelve", "Thirteen", "Fourteen", "Fifteen", "Sixteen", "Seventeen", "Eighteen", "Nineteen")
    DvacetAzDevadesat = Array("", "", "Twenty", "Thirty", "Forty", "Fifty", "Sixty", "Seventy", "Eighty", "Ninety")

    Jednotky(2) = "Thousand"
    Jednotky(3) = "Million"
    Jednotky(4) = "Billion"
    Jednotky(5) = "Trillion"

    Pom = ""

    For Pos = 15 To 3 Step -3
        If DelkaTextu >= Pos - 2 Then
            bNalezeno = False
            If DelkaTextu >= Pos Then
                iStovky = Asc(Mid$(Dolary, DelkaTextu - Pos + 1, 1)) - 48
                If iStovky > 0 Then
                    Pom = Pom & " " & JednaAzDevet(iStovky) & " Hundred"
                    bNalezeno = True
                End If
        End If
        iDesitky = 0
        iJednotky = 0

        If DelkaTextu >= Pos - 1 Then
            iDesitky = Asc(Mid$(Dolary, DelkaTextu - Pos + 2, 1)) - 48
        End If

        If DelkaTextu >= Pos - 2 Then
            iJednotky = Asc(Mid$(Dolary, DelkaTextu - Pos + 3, 1)) - 48
        End If

        If iDesitky = 1 Then
            Pom = Pom & " " & DesetAzDevatenact(iJednotky)
            bNalezeno = True
        Else
            If iDesitky >= 2 Then
                Pom = Pom & " " & DvacetAzDevadesat(iDesitky)
                bNalezeno = True
            End If
            If iJednotky > 0 Then
                If iDesitky >= 2 Then
                    Pom = Pom & "-"
                Else
                    Pom = Pom & " "
                End If
                Pom = Pom & JednaAzDevet(iJednotky)
                bNalezeno = True
            End If
        End If
        If bNalezeno And Pos > 3 Then
            Pom = Pom & " " & Jednotky(Pos \ 3)
        End If
    End If
    Next Pos

  VYPISVDOLARECH = Trim(Pom) & " and " & Centy
  If PriznakZapornehoCisla Then VYPISVDOLARECH = "(" & VYPISVDOLARECH & ")"
End Function

Attribute VB_Name = "Sheet1"
Attribute VB_Base = "0{00020820-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True


Attribute VB_Name = "ThisWorkbook"
Attribute VB_Base = "0{00020819-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True




INQUEST-PP=macro
