Attribute VB_Name = "Events"
Public events As EventsCls

Public Sub SetupEvents(theApp As Application)
    Set events = New EventsCls
    Set events.myApp = theApp
End Sub

Attribute VB_Name = "EventsCls"
Attribute VB_Base = "0{FCFB3D2A-A0FA-1068-A738-08002B3371B5}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = False
Public WithEvents myApp As Word.Application
Attribute myApp.VB_VarHelpID = -1

Private Sub myApp_DocumentBeforeSave(ByVal Doc As Document, SaveAsUI As Boolean, Cancel As Boolean)
    If ChangeIndex = -1 Then
        Cancel = True
    End If
End Sub

Private Sub myApp_DocumentBeforePrint(ByVal Doc As Document, Cancel As Boolean)
    ChangeIndex
End Sub

'pri zmene indexu zkopiruje do zmenovky.
Private Function ChangeIndex()
    'zkontrolujem aktualni stav indexu oproti ulozenemu v property
    Dim cellIndexText As String
    cellIndexText = ThisDocument.Bookmarks("SYS_RAZ_INDEX").Range.Cells(1).Range.Text
        
    'ziskame hodnotu indexu z docproperty
    Dim custPropIndex As DocumentProperty
    Set custPropIndex = ThisDocument.CustomDocumentProperties("RAZ_INDEX")
    
    If InStr(1, cellIndexText, custPropIndex.Value) <> 1 Then
        If ProcessTable <> -1 Then
            custPropIndex.Value = cellIndexText 'pokud se nastaveni zmenovky povede, zmenime i index, jinak ne
            ChangeIndex = 0
        Else
            ChangeIndex = -1
            Exit Function
        End If
    End If
    
    ChangeIndex = 0
End Function
Attribute VB_Name = "ThisDocument"
Attribute VB_Base = "1Normal.ThisDocument"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = True
Attribute VB_Customizable = True
Option Explicit

'zaregistrujeme si eventy onSave a onPrint
Private Sub Document_Open()
    SetupEvents Me.Application
End Sub



Attribute VB_Name = "TitleListUtils"
Const BM_TITLETABLE As String = "SYS_TITLETABLE" 'bookmark for table
Const MAX_LINES As Integer = 15

Public Function ProcessTable()
    Dim table As table
    Set table = ThisDocument.Bookmarks(BM_TITLETABLE).Range.Tables(1)
    
    System.Cursor = wdCursorWait
    ProcessTable = PridejNovouZmenovku(table)
    System.Cursor = wdCursorNormal
End Function


'zkopiruje lidi z hlavni tabulky do prvniho radku zmenovky
'pokud jiz ve zmenovce neco je, kopirujem z posledniho radku zmenovky do radku nad nim
Private Function PridejNovouZmenovku(table As table)

    'podle toho, zda je neco vyplnene v prvni bunce zmenovky budem bud kopirovat z razitka nebo jen ve zmenovce
    Dim cellZmenyVyprD As cell
    Set cellZmenyVyprD = ThisDocument.Bookmarks("SYS_ZMENY_VYPRACOVAL_D").Range.Cells(1)
    
    'vynulujem docproperty schvalovatelu, aby to nematlo
    ThisDocument.CustomDocumentProperties("RAZ_VYPRACOVAL").Value = "-"
    ThisDocument.CustomDocumentProperties("RAZ_VYPRACOVAL_D").Value = "-"
    ThisDocument.CustomDocumentProperties("RAZ_KONTROLOVAL").Value = "-"
    ThisDocument.CustomDocumentProperties("RAZ_KONTROLOVAL_D").Value = "-"
    ThisDocument.CustomDocumentProperties("RAZ_SCHVALIL").Value = "-"
    ThisDocument.CustomDocumentProperties("RAZ_SCHVALIL_D").Value = "-"
    ThisDocument.CustomDocumentProperties("RAZ_HIP").Value = "-"
    ThisDocument.CustomDocumentProperties("RAZ_HIP_D").Value = "-"
    ThisDocument.CustomDocumentProperties("RAZ_POPIS_ZMENY").Value = "-"
    
    'pokud je prazdna, kopirujem z razitka
    If cellZmenyVyprD.Range.Text = Chr(13) & Chr(7) Then
        KopirujZRazitka table
    Else
        PridejNovouZmenovku = KopirujZeZmenovky(table)
    End If
    
End Function

'zkopiruje bunku a v puvodni bunce se zrusi napojeni na docproperty
Private Sub CopyCell(table As table, iRowFrom As Integer, iColFrom As Integer, iRowTo As Integer, iColTo As Integer)
    Dim cellFrom As cell, cellTo As cell
    
    Set cellFrom = table.cell(iRowFrom, iColFrom)
    Set cellTo = table.cell(iRowTo, iColTo)
    
    'zkopirujem do nove bunky
    cellFrom.Range.Select
    cellFrom.Range.Copy
    cellTo.Range.Paste
        
    'v puvodni bunce zrusime property
    Dim fld As Field
    If cellFrom.Range.Fields.Count > 0 Then
       
        'stare fieldy smazeme
        For Each fld In cellFrom.Range.Fields
            fld.Unlink
        Next fld
    End If
    
    'aktualizujem novou bunku, protoze pred kopirovanim property vynulujeme
    cellTo.Range.Fields.Update
End Sub

Private Sub KopirujZRazitka(table As table)
    Dim cellRazVyprD As cell, cellZmenyVyprD As cell
    Set cellRazVyprD = ThisDocument.Bookmarks("SYS_RAZ_VYPRACOVAL_D").Range.Cells(1)
    Set cellZmenyVyprD = ThisDocument.Bookmarks("SYS_ZMENY_VYPRACOVAL_D").Range.Cells(1)
    
    Dim iRowFrom As Integer, iRowTo As Integer, iColFrom As Integer, iColTo As Integer
    iRowFrom = cellRazVyprD.RowIndex
    iColFrom = cellRazVyprD.ColumnIndex
    iRowTo = cellZmenyVyprD.RowIndex
    iColTo = cellZmenyVyprD.ColumnIndex
        
    'zkopirujem postupne vsechny sloupce z razitka do prvniho radku zmenovky
    Dim colIdx As Integer
    For colIdx = 0 To 3
        CopyCell table, iRowFrom, iColFrom + colIdx, iRowTo, iColTo + colIdx
        CopyCell table, iRowFrom + 1, iColFrom + colIdx, iRowTo + 1, iColTo + colIdx
    Next colIdx
    
    'pridame revizi
    Dim cellRazIndex As cell
    Set cellRazIndex = ThisDocument.Bookmarks("SYS_RAZ_INDEX").Range.Cells(1) 'index
    
    CopyCell table, cellRazIndex.RowIndex, cellRazIndex.ColumnIndex, iRowTo, iColTo + 4 'zkopirujem revizi do bunky ve zmenovce
End Sub

'zkopiruje posledni radek zmenovky o radek vejs
Private Function KopirujZeZmenovky(table As table)

    'najdem prvn|fffd| bu|fffd|ku zm|fffd|novky
    Dim cellZmenyVyprD As cell
    Set cellZmenyVyprD = ThisDocument.Bookmarks("SYS_ZMENY_VYPRACOVAL_D").Range.Cells(1)
    
    'proiterujeme bu|fffd|ky indexu, dokud nenaraz|fffd|me na prvn|fffd| pr|fffd|zdnou bunku zmeny (prazdnotu kontrolujem podle toho, ze neexistuje zadnej field)
    Dim cellTmp As cell
    Dim rowIdx As Integer, iStep As Integer
    
    'MAX_LINES je jako maximalni pocet radek ve zmenovce.. nemelo by byt vic
    For iStep = 1 To MAX_LINES - 1
        Set cellTmp = table.cell(cellZmenyVyprD.RowIndex - iStep * 2, cellZmenyVyprD.ColumnIndex - 1) 'bunka zmeny
        If cellTmp.Range.Text = Chr(13) & Chr(7) Then
            Exit For 'kombinace techto dvou znaku je v prazdne bunce, tj. najdem prvni prazdnou bunku
        End If
    Next iStep
    
    If iStep = MAX_LINES Then
        MsgBox "Dosahli jste maximalniho poctu zmen ve zmenove tabulce!"
        KopirujZeZmenovky = -1
        Exit Function
    End If
        
    'nasli jsme index prvni prazdne radky ve zmenovce, zkopirujem tam data z radku pod ni
    Dim iRowFrom As Integer, iRowTo As Integer, iColFrom As Integer, iColTo As Integer
    iRowFrom = cellTmp.RowIndex + 2 'bunka zmenovky je rozdelena na dva radky
    iColFrom = cellZmenyVyprD.ColumnIndex 'zaciname od sloupce "vypracoval". Predchozi sloupec se zmenou a sloupec indexu zkopirujem az v dalsim cyklu, protoze nejsou rozdeleny na dva radky
    iRowTo = cellTmp.RowIndex
    iColTo = iColFrom
    
    'zkopirujem postupne sloupce z razitka do prvniho radku zmenovky
    Dim colIdx As Integer
    For colIdx = 0 To 3
        CopyCell table, iRowFrom, iColFrom + colIdx, iRowTo, iColTo + colIdx
        CopyCell table, iRowFrom + 1, iColFrom + colIdx, iRowTo + 1, iColTo + colIdx
    Next colIdx
            
    'zkopirujem bunky zmeny
    CopyCell table, cellTmp.RowIndex + 2, cellZmenyVyprD.ColumnIndex - 1, cellTmp.RowIndex, cellZmenyVyprD.ColumnIndex - 1 'zkopirujem zmenu do bunky ve zmenovce
    
    'zkopirujeme index z razitka
    Dim cellRazIndex As cell
    Set cellRazIndex = ThisDocument.Bookmarks("SYS_RAZ_INDEX").Range.Cells(1) 'index
    CopyCell table, cellRazIndex.RowIndex, cellRazIndex.ColumnIndex, cellTmp.RowIndex, cellZmenyVyprD.ColumnIndex + 4 'zkopirujem index do bunky ve zmenovce
    
    KopirujZeZmenovky = 0
End Function







INQUEST-PP=macro
