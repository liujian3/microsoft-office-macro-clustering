Attribute VB_Name = "ThisDocument"
Attribute VB_Base = "0{00020906-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Option Explicit

' tato |fffd|ablona je upravena pro Fill.vbs verze 1.7

Sub CreateFillBar()
Dim cb As CommandBar
Dim cbxColNames As CommandBarComboBox
Dim cbtInsertCol As CommandBarButton

On Error Resume Next
Set cb = CommandBars("Fill")
If Err.Number <> 0 Then
   Err.Clear
   On Error GoTo 0
Else
   MsgBox "Pokud chcete toolbar Fill znovu vytvo|fffd|it, tak jej nejd|fffd||fffd|ve zru|fffd|te"
   Exit Sub
End If

Set cb = CommandBars.Add("Fill")
cb.Visible = True
Set cbxColNames = cb.Controls.Add(msoControlComboBox)
With cbxColNames
   .Width = PointsToPixels(MillimetersToPoints(35), False)
   .DropDownWidth = -1
   .AddItem "Prijmeni"
   .AddItem "Jmeno"
   .AddItem ".If <podm> .EndIf"
   .AddItem ".Else"
   .AddItem ".ForEachRow .EndFor"
   .AddItem "& <p|fffd||fffd|kaz>"
   .AddItem ".Lock .EndLock"
   .ListIndex = 1
End With
Set cbtInsertCol = cb.Controls.Add(msoControlButton)
With cbtInsertCol
   .Style = msoButtonCaption
   .Caption = "Vlo|fffd|"
   .DescriptionText = "Vlo|fffd|it n|fffd|zev sloupce"
   .OnAction = "InsertColumn"
End With
Set cbtInsertCol = cb.Controls.Add(msoControlButton)
With cbtInsertCol
   .Style = msoButtonCaption
   .Caption = "Na|fffd|ti seznam"
   .OnAction = "ReadColNames"
End With
Set cbtInsertCol = cb.Controls.Add(msoControlButton)
With cbtInsertCol
   .Style = msoButtonCaption
   .Caption = "Zv|fffd|razni pole"
   .OnAction = "HighlightAllFields"
End With
Set cbtInsertCol = cb.Controls.Add(msoControlButton)
With cbtInsertCol
   .Style = msoButtonCaption
   .Caption = "Zpracuj dokument"
   .OnAction = "ExecuteFill"
End With
End Sub

Sub InsertColumn()
Dim cbxColNames As CommandBarComboBox
Dim ColName As String
Dim f As Field
Set cbxColNames = CommandBars("Fill").Controls(1)
ColName = cbxColNames.text
Select Case Left(ColName, 1)
Case "."
   If ColName = ".Else" Then
      InsertElseField
   ElseIf Left(ColName, 5) = ".Lock" Then
      InsertLockField
   Else ' vlo|fffd|en|fffd| blokov|fffd|ho p|fffd||fffd|kazu
      InsertBlockFields ColName
   End If
Case "&" ' vlo|fffd|en|fffd| p|fffd||fffd|kazu execute
   InsertExecField ColName
Case Else ' vlo|fffd|en|fffd| n|fffd|zvu sloupce nebo v|fffd|razu eval
   ColName = "? " & ColName
   InsertUserField Selection.Range, ColName
End Select
End Sub

Sub InsertElseField()
Dim r As Range
Set r = Selection.Paragraphs(1).Range
r.InsertParagraphBefore
r.Collapse wdCollapseStart
InsertUserField r, ".Else"
End Sub

Sub InsertBlockFields(blok As String)
Dim r As Range
Dim p1 As Paragraph, p2 As Paragraph
Dim a As Variant

a = Split(blok, ".")
If a(1) = "ForEachRow " Then
   If Selection.Information(wdInHeaderFooter) Then
      MsgBox "Do z|fffd|hlav|fffd|/z|fffd|pat|fffd| nelze vkl|fffd|dat blokov|fffd| p|fffd||fffd|kaz .ForEachRow"
      Exit Sub
   End If
End If
Set p1 = Selection.Paragraphs(1)
Set p2 = Selection.Paragraphs(Selection.Paragraphs.Count)
Set r = p1.Range
' vlo|fffd|en|fffd| nov|fffd|ho odstavce p|fffd|ed
r.InsertParagraphBefore
r.Collapse wdCollapseStart
InsertUserField r, "." & a(1)
Set r = p2.Range
r.InsertParagraphAfter
Set r = r.Paragraphs(2).Range
r.Collapse wdCollapseStart
InsertUserField r, "." & a(2)
End Sub

Sub InsertLockField()
Dim r As Range
Dim p1 As Paragraph, p2 As Paragraph

If Selection.Information(wdInHeaderFooter) Then
   MsgBox "Do z|fffd|hlav|fffd|/z|fffd|pat|fffd| nelze vkl|fffd|dat blokov|fffd| p|fffd||fffd|kaz .Lock"
   Exit Sub
End If
Set p1 = Selection.Paragraphs(1)
Set p2 = Selection.Paragraphs(Selection.Paragraphs.Count)
Set r = p1.Range
' vlo|fffd|en|fffd| nov|fffd|ho odstavce p|fffd|ed
r.Collapse wdCollapseStart
InsertUserField r, ".Lock"
Set r = p2.Range
r.Collapse wdCollapseEnd
InsertUserField r, ".EndLock"
End Sub

Sub InsertExecField(cmd As String)
Dim r As Range
Set r = Selection.Paragraphs(1).Range
r.InsertParagraphBefore
r.Collapse wdCollapseStart
InsertUserField r, cmd
End Sub

Sub HighlightAllFields()
Dim f As Field
For Each f In ActiveDocument.Fields
   If f.Type = wdFieldEmpty Then
      HighlightField f
   End If
Next
End Sub

Sub InsertUserField(r As Range, text As String)
Dim f As Field
Set f = ActiveDocument.Fields.Add(r, wdFieldEmpty)
f.Code.Delete
f.Code.InsertBefore text
HighlightField f
End Sub

Sub HighlightField(f As Field)
Select Case Left(Trim(f.Code.text), 1)
Case "?": f.Code.HighlightColorIndex = wdYellow
Case ".": f.Code.HighlightColorIndex = wdBrightGreen
Case "&": f.Code.HighlightColorIndex = wdTurquoise
End Select
End Sub

Sub ReadColNames()
Dim cbxColNames As CommandBarComboBox
Dim fso As FileSystemObject
Dim txtStr As TextStream
Dim InputData As String
Dim r As String, a As Variant, i As Integer

If MsgBox("Chcete skute|fffd|n|fffd| na|fffd||fffd|st seznam sloupc|fffd| z datov|fffd|ho souboru", vbQuestion + vbYesNo) = vbNo Then
   Exit Sub
End If
Set cbxColNames = CommandBars("Fill").Controls(1)
Set fso = New FileSystemObject
InputData = ActiveDocument.Path & "\data.txt"
Set txtStr = fso.OpenTextFile(InputData)
r = txtStr.ReadLine
a = Split(r, "|")
With cbxColNames
   .Clear
   For i = 0 To UBound(a)
      .AddItem a(i)
   Next
   .AddItem ".If <podm> .EndIf"
   .AddItem ".Else"
   .AddItem ".ForEachRow .EndFor"
   .AddItem "& <p|fffd||fffd|kaz>"
   .AddItem ".Lock .EndLock"
   .ListIndex = 1
End With
txtStr.Close
End Sub

Sub ExecuteFill()
Dim x As Double
Dim fill As String
If Application.Documents.Count = 0 Then
   MsgBox "Tato funkce vy|fffd|aduje otev|fffd|en|fffd| dokument"
   Exit Sub
End If
fill = """" & ActiveDocument.Path & "\fill.vbs"""
x = Shell("wscript.exe " & fill, vbHide)
End Sub



INQUEST-PP=macro
