Attribute VB_Name = "Add_Appointments_SM"
Option Explicit
Option Private Module

Sub AddAppointments()
' written by Scott Mygind on 11/8/2001
  
  Dim myAllDayEvent As Boolean
  Dim myBody As String
  Dim myBusyStatusText As String
  Dim myCounter As Integer
  Dim myEndText As String
  Dim myLocation As String
  Dim myReminderSet As Boolean
  Dim myStart As Date
  Dim mySubject As String
  Dim myText As String
  Dim c As Integer
  Dim r As Integer
  Dim myOlApp, myNameSpace, myFolder, myMsg, myBusyStatus, myReminderTime, myDuration, myItem
  
  Set myOlApp = CreateObject("Outlook.Application")
  Set myItem = myOlApp.CreateItem(1)
  Set myNameSpace = myOlApp.GetNamespace("MAPI")
  Set myFolder = myNameSpace.GetDefaultFolder(9)

  [A8].Select
  myCounter = 0: r = 7: c = 1
  Do
    Set myOlApp = CreateObject("Outlook.Application")
    Set myItem = myOlApp.CreateItem(1)
    r = r + 1
    c = 1
    If Cells(r, c).Value <> "" Then
      mySubject = Cells(r, c)
      myLocation = Cells(r, c + 1).Value
      myStart = Cells(r, c + 2).Value
      myDuration = Cells(r, c + 3).Value
      myBusyStatusText = Cells(r, c + 4).Value
      Select Case myBusyStatusText
        Case "Free"
          myBusyStatus = 0
        Case "Tentative"
          myBusyStatus = 1
        Case "Busy"
          myBusyStatus = 2
        Case "Out of Office"
          myBusyStatus = 3
        Case Else
          MsgBox "There was an incorrect 'Busy Status'. Please re-check before trying again."
          End
      End Select
      myReminderSet = Cells(r, c + 5).Value
      myReminderTime = Cells(r, c + 6).Value
      myAllDayEvent = Cells(r, c + 7).Value
      myBody = Cells(r, c + 8).Value
      With myItem
         .Subject = mySubject
         .Start = myStart
         If myLocation <> "" Then .Location = myLocation
         If myDuration <> "" Then .Duration = myDuration
         .BusyStatus = myBusyStatus
         .ReminderSet = myReminderSet
         If myReminderSet = True Then
           .ReminderMinutesBeforeStart = myReminderTime
         End If
         .AllDayEvent = myAllDayEvent
         If myBody <> "" Then .Body = myBody
      End With
      myItem.Save
      myCounter = myCounter + 1
    End If
  Loop Until Cells(r, c).Value = ""

  If myCounter > 1 Or myCounter = 0 Then
    myEndText = " appointments."
  Else
    myEndText = " appointment."
  End If
    
  Set myOlApp = Nothing
  Set myItem = Nothing
  Set myNameSpace = Nothing
  Set myFolder = Nothing
    
  [A8].Select
  myText = "Added " & myCounter & myEndText & vbCr & vbCr & "Have a nice day!"
  myMsg = MsgBox(myText, vbOKOnly, "Just to let you know...")

End Sub

Sub DeleteAppointments()
' written by Scott Mygind on 1/6/2005

  Dim myAllDayEvent As Boolean
  Dim myBusyStatus As Integer
  Dim myBusyStatusText As String
  Dim myCounter As Integer
  Dim myEndText As String
  Dim myFilter As String
  Dim myStart As Date
  Dim mySubject As String
  Dim myText As String
  Dim c As Integer
  Dim r As Integer
  Dim myOlApp, myNameSpace, myFolder, myMsg, myItm, myItms, myRestrict, myNewRestrict
  
  Set myOlApp = CreateObject("Outlook.Application")
  Set myNameSpace = myOlApp.GetNamespace("MAPI")
  Set myFolder = myNameSpace.GetDefaultFolder(9)
  Set myItms = myNameSpace.GetDefaultFolder(9).Items

  myCounter = 0: r = 7: c = 1
  [A8].Select
  Do
    r = r + 1
    c = 1
    If Cells(r, c).Value <> "" Then
      mySubject = Cells(r, c).Value
      myStart = Cells(r, c + 2).Value
      myBusyStatusText = Cells(r, c + 4).Value
      Select Case myBusyStatusText
        Case "Free"
          myBusyStatus = 0
        Case "Tentative"
          myBusyStatus = 1
        Case "Busy"
          myBusyStatus = 2
        Case "Out of Office"
          myBusyStatus = 3
        Case Else
          MsgBox "There was an incorrect 'Busy Status'. Please re-check before trying again."
          End
      End Select
      myAllDayEvent = Cells(r, c + 7).Value
      If myAllDayEvent = False Then
        myFilter = "[Start] >= '" & Format(myStart, "ddddd h:nn AMPM") & "'"
      Else
        myFilter = "[Start] >= '" & Format(myStart - 1, "ddddd h:nn AMPM") & "'"
      End If
      Set myRestrict = myItms.Restrict(myFilter)
      myRestrict.Sort "[Start]"
      myFilter = "[Subject] = """ & mySubject & """"
      Set myNewRestrict = myRestrict.Restrict(myFilter)
      myFilter = "[AllDayEvent] = " & myAllDayEvent
      Set myNewRestrict = myNewRestrict.Restrict(myFilter)
      myFilter = "[BusyStatus] = " & myBusyStatus
      Set myNewRestrict = myNewRestrict.Restrict(myFilter)
      
      For Each myItm In myNewRestrict
        myItm.Delete
        myCounter = myCounter + 1
        Exit For
      Next
    End If
  Loop Until Cells(r, c).Value = ""

  If myCounter > 1 Or myCounter = 0 Then
    myEndText = " appointments."
  Else
    myEndText = " appointment."
  End If
    
  [A7].Select
  myText = "Deleted " & myCounter & myEndText & vbCr & vbCr & "Have a nice day!"
  myMsg = MsgBox(myText, vbOKOnly, "Just to let you know...")

  Set myItms = Nothing
  Set myFolder = Nothing
  Set myNameSpace = Nothing
  Set myOlApp = Nothing
  
End Sub
Attribute VB_Name = "Sheet1"
Attribute VB_Base = "0{00020820-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Attribute VB_Control = "cmdDeleteAppointments, 7, 0, MSForms, CommandButton"
Attribute VB_Control = "cmdAddAppointments, 1, 1, MSForms, CommandButton"
Private Sub cmdAddAppointments_Click()
  AddAppointments
End Sub

Private Sub cmdDeleteAppointments_Click()
  DeleteAppointments
End Sub
Attribute VB_Name = "ThisWorkbook"
Attribute VB_Base = "0{00020819-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True

' InQuest injected base64 decoded content
' +y+Z

INQUEST-PP=macro
