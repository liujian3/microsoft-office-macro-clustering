Attribute VB_Name = "ThisDocument"
Attribute VB_Base = "0{00020906-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Option Explicit
Dim kundendaten As Variant

Private Sub document_new()
Dim excelmappe As Object
Dim excelinstanz As Object
Dim excelsheet As Object
Dim datei As String
Dim letztezeile As Long, letztespalte As Long, i As Long, j As Long
'Dim eintrag As String

On Error GoTo fehler
'Excel-Mappe |fffd|ffnen
Set excelinstanz = CreateObject("Excel.application")
excelinstanz.Visible = False
datei = ThisDocument.Path & "\kundenliste.xlsx"

Set excelmappe = excelinstanz.workbooks.Open(FileName:=datei)
Set excelsheet = excelmappe.sheets("tabelle1")

    'letzte Zeile / letzte Spalte feststellen
    With excelsheet
        letztezeile = .Cells(.Rows.Count, 1).End(-4162).Row
        letztespalte = .Cells(1, .Columns.Count).End(-4159).Column
       
        'ExcelListe in Variable Kundendaten |fffd|bertragen
        kundendaten = .Range(.Cells(2, 1), .Cells(letztezeile, letztespalte))
     End With
'
 'Ecelmappe schlie|fffd|en:
excelmappe.Close savechanges:=False
excelinstanz.Visible = True
excelinstanz.Quit
Set excelinstanz = Nothing
Set excelmappe = Nothing

Exit Sub
fehler:
MsgBox Err.Description & " - " & Err.Number
excelinstanz.Visible = True
excelinstanz.Quit
Set excelinstanz = Nothing
Set excelmappe = Nothing
End Sub

Private Sub Document_ContentControlOnExit(ByVal ContentControl As ContentControl, Cancel As Boolean)
Dim kdNr As String
Dim i As Long
Dim treffer As Boolean

treffer = False

'Nur beim richtigen Contentcontrol loslegen
If ContentControl.Tag <> "KundenNr" Then Exit Sub

'Pr|fffd|fen ob eingegebene KD-Nr vorhanden
kdNr = ContentControl.Range.Text

    For i = 1 To UBound(kundendaten)
        
        'raus aus der Schleife, wenn Kundennummer gefunden
        If kundendaten(i, 1) = kdNr Then
            treffer = True
            Exit For
        End If
    Next i

    'Abbruch, wenn Kundennummer nicht gefunden
    If treffer = False Then
        MsgBox "Die eingegebene Kundennummer existiert nicht." & vbLf & _
        "Bitte andere Kundennummer eingeben."
        Exit Sub
    End If

'Textmarken f|fffd|llen
With ActiveDocument

    If .Bookmarks.Exists("kunde") Then
        .Bookmarks("kunde").Range.Select
        Selection = kundendaten(i, 2)
        .Bookmarks.Add Name:="kunde", Range:=Selection.Range
    Else
        MsgBox "Die Textmarke 'kunde' existiert nicht!"
        Exit Sub
    End If

    If .Bookmarks.Exists("teldirekt") Then
        .Bookmarks("teldirekt").Range.Select
        Selection = kundendaten(i, 3)
        .Bookmarks.Add Name:="teldirekt", Range:=Selection.Range
    Else
        MsgBox "Die Textmarke 'deldirekt' existiert nicht!"
        Exit Sub
    End If


    If .Bookmarks.Exists("email") Then
        .Bookmarks("email").Range.Select
        Selection = kundendaten(i, 4)
        .Bookmarks.Add Name:="email", Range:=Selection.Range
    Else
        MsgBox "Die Textmarke 'email' existiert nicht!"
        Exit Sub
    End If
End With
End Sub


INQUEST-PP=macro
