Attribute VB_Name = "Feuil1"
Attribute VB_Base = "0{00020820-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Option Explicit

Attribute VB_Name = "Feuil2"
Attribute VB_Base = "0{00020820-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Option Explicit

Attribute VB_Name = "Feuil3"
Attribute VB_Base = "0{00020820-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Option Explicit
Sub XMail()
    MailOutlookForm.Show 0
End Sub

Attribute VB_Name = "MailOutlookForm"
Attribute VB_Base = "0{30CE045A-DED0-4D9F-80D4-237221483115}{336BE7CC-6094-4120-8089-948E73670B5E}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = False
Option Explicit
Dim cBar As CommandBar, Cmb As CommandBarControl
Dim i As Integer, Rv As String, k

Private Sub UserForm_Initialize()
    Set Ws1 = ThisWorkbook.Sheets("ConfigMail")
    Set Ws2 = ThisWorkbook.Sheets("Contact")
'#Taille de l'UserForm
    Me.Width = "276,75"
    Me.Height = "401,25"
'#FaceId Icone sur les boutons
    Set cBar = CommandBars.Add(Position:=msoBarFloating, MenuBar:=False, temporary:=True)
    Set Cmb = cBar.Controls.Add(Type:=msoControlButton, temporary:=True)
'#FaceId Bouton Fermer
    Cmb.FaceId = 1786
    Me.BFermer.Picture = Cmb.Picture
    Me.BFermer.PicturePosition = fmPicturePositionCenter
'#FaceId Bouton Nouveau
    Cmb.FaceId = 3792
    Me.Nouveau.Picture = Cmb.Picture
    Me.Nouveau.PicturePosition = fmPicturePositionCenter
    Me.Nouveau.ControlTipText = "Nouveau mail"
'#FaceId Bouton Envoyer
    Cmb.FaceId = 2568
    Me.BEnvoyer.Picture = Cmb.Picture
    Me.BEnvoyer.PicturePosition = fmPicturePositionCenter
'#FaceId Bouton Pi|fffd|ce jointe
    Cmb.FaceId = 1079 '2188
    Me.BPJointe.Picture = Cmb.Picture
    Me.BPJointe.PicturePosition = fmPicturePositionCenter
'#FaceId Bouton Configuration
    Cmb.FaceId = 2946
    Me.Config.Picture = Cmb.Picture
    Me.Config.PicturePosition = fmPicturePositionCenter
'#FaceId Bouton Pr|fffd|visualiser
    Cmb.FaceId = 318
    Me.Previsualiser.Picture = Cmb.Picture
    Me.Previsualiser.PicturePosition = fmPicturePositionCenter
'#FaceId Bouton ListeContact
    Cmb.FaceId = 24
    Me.ListeContact.Picture = Cmb.Picture
    Me.ListeContact.PicturePosition = fmPicturePositionCenter
    
'#FaceId Gras
    Cmb.FaceId = 113
    Me.Label1.Picture = Cmb.Picture
    Me.Label1.PicturePosition = fmPicturePositionCenter
'#FaceId Italic
    Cmb.FaceId = 114
    Me.Label2.Picture = Cmb.Picture
    Me.Label2.PicturePosition = fmPicturePositionCenter
'#FaceId Soulignement simple
    Cmb.FaceId = 115
    Me.Label3.Picture = Cmb.Picture
    Me.Label3.PicturePosition = fmPicturePositionCenter
'#FaceId Taille text
    Cmb.FaceId = 254
    Me.Label4.Picture = Cmb.Picture
    Me.Label4.PicturePosition = fmPicturePositionCenter
'#FaceId Lien
    Cmb.FaceId = 1577
    Me.Label5.Picture = Cmb.Picture
    Me.Label5.PicturePosition = fmPicturePositionCenter
'#FaceId LienMail
    Cmb.FaceId = 794
    Me.Label6.Picture = Cmb.Picture
    Me.Label6.PicturePosition = fmPicturePositionCenter
    
'#Liste des contacts
    If Ws2.Cells(2, 1) = Empty Then
        Me.ListeContact.Visible = False
        Me.TextBox1.Width = "186"
    Else
        Me.ListeContact.Visible = True
        Me.TextBox1.Width = "162"
    End If
'#Signature
    Me.TextBox5.Value = Ws1.Range("A3").Value
'#Supprime mail apr|fffd|s envoie
    If Ws1.Range("A5").Value = True Then
        Me.CheckBox1.Value = True
        Me.CheckBox1.Caption = "Supprimer le mail apr|fffd|s envoie - Oui"
    Else
        Me.CheckBox1.Value = False
        Me.CheckBox1.Caption = "Supprimer le mail apr|fffd|s envoie - Non"
    End If
'#Format Html
    If Ws1.Range("A9").Value = True Then
        Me.CheckBox2.Value = True
        Me.CheckBox2.Caption = "Html Oui"
    Else
        Me.CheckBox2.Value = False
        Me.CheckBox2.Caption = "Html Non"
    End If
'#Priorit|fffd|
    Me.ComboBox1.AddItem "Hight"
    Me.ComboBox1.AddItem "Normal"
    Me.ComboBox1.AddItem "Low"
    If Ws1.Range("A7").Value = "Hight" Then Me.ComboBox1.Value = "Hight"
    If Ws1.Range("A7").Value = "Normal" Then Me.ComboBox1.Value = "Normal"
    If Ws1.Range("A7").Value = "Low" Then Me.ComboBox1.Value = "Low"
'#Html
    If Me.CheckBox2.Value = True Then
        Me.TextBox4.Height = "210,75"
        Me.TextBox4.Width = "186"
        Me.TextBox4.Top = "119,25"
        Me.Message.Top = "120"
        For i = 1 To 6
            Me.Controls("Label" & i).Visible = True
        Next i
        Me.Previsualiser.Visible = True
    Else
        Me.TextBox4.Height = "240"
        Me.TextBox4.Width = "186"
        Me.TextBox4.Top = "90"
        Me.Message.Top = "96"
        For i = 1 To 6
            Me.Controls("Label" & i).Visible = False
        Next i
        Me.Previsualiser.Visible = False
    End If
End Sub
'
'#--- TextBox ---
'
'#Sujet
Private Sub TextBox3_Change()
'#Met la premi|fffd|re lettre en majuscule
Dim st As String
    st = Me.TextBox3.Text
    Me.TextBox3.Text = UCase(Mid(st, 1, 1)) & Mid$(st, 2, Len(st))
    Me.TextBox3.SelStart = Len(st)
End Sub
'#Corp du message
Private Sub TextBox4_Change()
'#Met la premi|fffd|re lettre en majuscule
Dim st As String
    st = Me.TextBox4.Text
    Me.TextBox4.Text = UCase(Mid(st, 1, 1)) & Mid$(st, 2, Len(st))
    Me.TextBox4.SelStart = Len(st)
End Sub
'
'#--- Label ---
'
'Gras
Private Sub Label1_Click()
    If Me.TextBox4.SelText <> "" Then
        Rv = Me.TextBox4.SelText
        Me.TextBox4.SelText = "<b>" & Rv & "</b>"
    Else
        MsgBox "Vous devez s|fffd|lectionner le texte |fffd| mettre en gras.", vbCritical, "Erreur"
    End If
End Sub
'Italic
Private Sub Label2_Click()
    If Me.TextBox4.SelText <> "" Then
        Rv = Me.TextBox4.SelText
        Me.TextBox4.SelText = "<i>" & Rv & "</i>"
    Else
        MsgBox "Vous devez s|fffd|lectionner le texte |fffd| mettre en italic.", vbCritical, "Erreur"
    End If
End Sub
'Souligner
Private Sub Label3_Click()
    If Me.TextBox4.SelText <> "" Then
        Rv = Me.TextBox4.SelText
        Me.TextBox4.SelText = "<u>" & Rv & "</u>"
    Else
        MsgBox "Vous devez s|fffd|lectionner le texte |fffd| mettre en souligner.", vbCritical, "Erreur"
    End If
End Sub
'Taille texte
Private Sub Label4_Click()
Dim TT As String
    TT = InputBox("Quel taille du texte ?", "Taille du texte")
    If TT = Empty Then Exit Sub
    If Me.TextBox4.SelText <> "" Then
        Rv = Me.TextBox4.SelText
        Me.TextBox4.SelText = "<font size=""" & TT & """>" & Rv & "</font>"
    Else
        MsgBox "Vous devez s|fffd|lectionner le texte pour mettre la taille.", vbCritical, "Erreur"
    End If
End Sub
'Lien
Private Sub Label5_Click()
Dim Lien As String
    Lien = InputBox("Mettre l'adresse du site :", "Ajouter un lien", "http://")
    If Lien = Empty Then Exit Sub
    If Me.TextBox4.SelText <> "" Then
        Rv = Me.TextBox4.SelText
        Me.TextBox4.SelText = "<a href=""" & Lien & """>" & Rv & "</a>"
    Else
        MsgBox "Vous devez s|fffd|lectionner le texte pour mettre le lien.", vbCritical, "Erreur"
    End If
End Sub
'Mail
Private Sub Label6_Click()
Dim LienMail As String
    LienMail = InputBox("Mettre l'adresse mail :", "Ajouter une adresse mail")
    If LienMail = Empty Then Exit Sub
    If Me.TextBox4.SelText <> "" Then
        Rv = Me.TextBox4.SelText
        Me.TextBox4.SelText = "<a href=mailto:""" & LienMail & """>" & Rv & "</a>"
    Else
        MsgBox "Vous devez s|fffd|lectionner le texte pour mettre le mail.", vbCritical, "Erreur"
    End If
End Sub
'
'#--- Bouton ---
'#Liste des contacts
Private Sub ListeContact_Click()
    MailOutlookListeForm.Show
End Sub
'#Piece jointe
Private Sub BPJointe_Click()
Dim Tableau, MultiSelectedItem As Variant, i As Integer
    ChDir ThisWorkbook.Path
    Me.ListBox1.Clear
    Me.NBFJ.Caption = ""
    With Application.FileDialog(msoFileDialogOpen)
        'D|fffd|finit un titre pour la bo|fffd|te de dialogue
        .Title = "S|fffd|lectionner la/les pi|fffd|ce(s) jointe(s) :"
        'Multi-s|fffd|lection
        .AllowMultiSelect = True
        'Efface les filtres existants.
        .Filters.Clear
        'D|fffd|finit une liste de filtres pour le champ "Type de fichiers".
        .Filters.Add "Excel", "*.xls;*.xlsx; *.xlsm; *.xlsb"
        .Filters.Add "Fichier archive", "*.zip; *.rar"
        .Filters.Add "Fichier texte", "*.txt"
        .Filters.Add "Pdf", "*.pdf"
        .Filters.Add "Word", "*.doc; *.docx"
        .Filters.Add "Tous les fichiers", "*.*"
        .FilterIndex = 1
        If .Show = -1 Then
            i = 0
            For Each MultiSelectedItem In .SelectedItems
                Tableau = Split(MultiSelectedItem, "\")
                Me.ListBox1.AddItem MultiSelectedItem
                Me.ListBox1.List(i, 1) = Tableau(UBound(Tableau))
                i = i + 1
            Next MultiSelectedItem
        End If
    End With
    Me.NBFJ.Caption = Me.ListBox1.ListCount & " "
End Sub
'#Pr|fffd|visualiser
Private Sub Previsualiser_Click()
    If Me.TextBox4.Value = Empty Then
        MsgBox "Il y a rien a pr|fffd|visualiser ..."
    Else
        PrevisualiserForm.Show
    End If
End Sub
'#Envoyer
Private Sub BEnvoyer_Click()
    'Coloration des Labels en noir
    Destinataire.ForeColor = RGB(0, 0, 0)
    Sujet.ForeColor = RGB(0, 0, 0)
    Message.ForeColor = RGB(0, 0, 0)
    'Contr|fffd|le de contenu
    If Me.TextBox1.Value = Empty Then
        Me.Destinataire.ForeColor = RGB(255, 0, 0)
        Me.TextBox1.SetFocus
    ElseIf Me.TextBox3.Value = Empty Then
        Me.Sujet.ForeColor = RGB(255, 0, 0)
        Me.TextBox3.SetFocus
    ElseIf Me.TextBox4.Value = Empty Then
        Me.Message.ForeColor = RGB(255, 0, 0)
        Me.TextBox4.SetFocus
    Else
        Envoyer_Mail_Outlook
        Raz
    End If
End Sub
'#Fermer
Private Sub BFermer_Click()
    VideVariable
    Unload Me
End Sub
'#Nouveau
Private Sub Nouveau_Click()
    Raz
End Sub
'#Configuration
Private Sub Config_Click()
    If Me.Config = True Then
        Me.Height = "508,5"
    Else
        Me.Height = "401,25"
    End If
End Sub
'
'#--- Configuration ---
'
'#Signature
Private Sub TextBox5_Change()
'#Met la premi|fffd|re lettre en majuscule
Dim st As String
    st = Me.TextBox5.Text
    Me.TextBox5.Text = UCase(Mid(st, 1, 1)) & Mid$(st, 2, Len(st))
    Me.TextBox5.SelStart = Len(st)
'#Ajoute la signature sur la feuille
    Ws1.Range("A3").Value = Me.TextBox5.Value
End Sub
'#Priorit|fffd|
Private Sub ComboBox1_Change()
    If Me.ComboBox1.Value = "Hight" Then Ws1.Range("A7").Value = "Hight"
    If Me.ComboBox1.Value = "Normal" Then Ws1.Range("A7").Value = "Normal"
    If Me.ComboBox1.Value = "Low" Then Ws1.Range("A7").Value = "Low"
End Sub
'#Supprimer le mail
Private Sub CheckBox1_Click()
    If Me.CheckBox1.Value = True Then
        Ws1.Range("A5").Value = True
        Me.CheckBox1.Caption = "Supprimer le mail apr|fffd|s envoie - Oui"
    Else
        Ws1.Range("A5").Value = False
        Me.CheckBox1.Caption = "Supprimer le mail apr|fffd|s envoie - Non"
    End If
End Sub
'#Html
Private Sub CheckBox2_Click()
    If Me.CheckBox2.Value = True Then
        Ws1.Range("A9").Value = True
        Me.CheckBox2.Caption = "Html Oui"
        Me.TextBox4.Height = "210,75"
        Me.TextBox4.Width = "186"
        Me.TextBox4.Top = "119,25"
        Me.Message.Top = "120"
        For i = 1 To 6
            Me.Controls("Label" & i).Visible = True
        Next i
        Me.Previsualiser.Visible = True
    Else
        Ws1.Range("A9").Value = False
        Me.CheckBox2.Caption = "Html Non"
        Me.TextBox4.Height = "240"
        Me.TextBox4.Width = "186"
        Me.TextBox4.Top = "90"
        Me.Message.Top = "96"
        For i = 1 To 6
            Me.Controls("Label" & i).Visible = False
        Next i
        Me.Previsualiser.Visible = False
    End If
End Sub
'----------------------------------------------------
'#Vide les variables
Private Sub VideVariable()
    Set cBar = Nothing
    Set Cmb = Nothing
    Set Ws1 = Nothing
End Sub
'#Remise |fffd| zero
Private Sub Raz()
    For i = 1 To 4
        Me.Controls("TextBox" & i).Value = Empty
    Next i
    Me.ListBox1.Clear
    Me.NBFJ.Caption = ""
    Destinataire.ForeColor = RGB(0, 0, 0)
    CopieCacher.ForeColor = RGB(0, 0, 0)
    Sujet.ForeColor = RGB(0, 0, 0)
    Message.ForeColor = RGB(0, 0, 0)
End Sub


Attribute VB_Name = "MailOutlookListeForm"
Attribute VB_Base = "0{8FE4710A-296F-4119-B266-AEE22E4CD0EF}{0EEE27DC-6755-4507-9584-BFA354D94938}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = False
Option Explicit
Dim k As Integer
Dim i As Variant

Private Sub UserForm_Initialize()
Set Ws2 = ThisWorkbook.Sheets("Contact")
    Me.ListBox1.Height = "163"
    Me.ListBox1.Width = "323,75"
    Me.ListBox1.ColumnCount = 3
    Me.ListBox1.ColumnWidths = "80;80;160"
    
    Me.ComboBox1.AddItem "Mail 1"
    Me.ComboBox1.AddItem "Mail 2"
    Me.ComboBox1.ListIndex = 0
End Sub
Private Sub UserForm_Activate()
    Mail1
End Sub

Private Sub ListBox1_Click()
    If MailOutlookForm.TextBox1.Value = Empty Then
        MailOutlookForm.TextBox1.Value = Me.ListBox1.List(, 2)
    Else
        MailOutlookForm.TextBox1.Value = MailOutlookForm.TextBox1.Value & ";" & Me.ListBox1.List(, 2)
    End If
End Sub
Private Sub ComboBox1_Change()
    If Me.ComboBox1.Value = "Mail 1" Then
        Mail1
    Else
        Mail2
    End If
End Sub

Private Sub Mail1()
Me.ListBox1.Clear
    k = 0
    For i = 2 To Ws2.[A65000].End(xlUp).Row
        Me.ListBox1.AddItem
        Me.ListBox1.List(k, 0) = Ws2.Cells(i, 1) 'Correspond |fffd| la colone A
        Me.ListBox1.List(k, 1) = Ws2.Cells(i, 2) 'Correspond |fffd| la colone B
        Me.ListBox1.List(k, 2) = Ws2.Cells(i, 3) 'Correspond |fffd| la colone C
        Me.ListBox1.List(k, 3) = i
        k = k + 1
    Next i
    Me.Caption = "Liste des contacts (" & k & ")"
    i = Empty
    k = Empty
End Sub
Private Sub Mail2()
Me.ListBox1.Clear
    k = 0
    For i = 2 To Ws2.[A65000].End(xlUp).Row
        Me.ListBox1.AddItem
        Me.ListBox1.List(k, 0) = Ws2.Cells(i, 1) 'Correspond |fffd| la colone A
        Me.ListBox1.List(k, 1) = Ws2.Cells(i, 2) 'Correspond |fffd| la colone B
        Me.ListBox1.List(k, 2) = Ws2.Cells(i, 4) 'Correspond |fffd| la colone D
        Me.ListBox1.List(k, 3) = i
        k = k + 1
    Next i
    Me.Caption = "Liste des contacts (" & k & ")"
    i = Empty
    k = Empty
End Sub





Attribute VB_Name = "PrevisualiserForm"
Attribute VB_Base = "0{33459ECE-373A-4B25-9A2D-522BF94C2F5F}{4025AD77-B03E-48B6-BFF6-59F61F174D48}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = False
Option Explicit

'Placez un contr|fffd|le WebBrowser dans L'UserForm.
'N|fffd|cessite les biblioth|fffd|ques:
'   *Microsoft Internet Controls
'   *Microsoft HTML Object Library

Private Sub UserForm_Initialize()
Dim AjoutFonction As Variant
Dim Fichier As String
Dim Message As String
Dim Tableau As Variant
Dim i As Integer
    
'------------------------------------------------------------------
    'Fichier = "http://vb.developpez.com/cours/images/cours_vb.gif"
    Message = MailOutlookForm.TextBox4.Value
'------------------------------------------------------------------
    
    'Cr|fffd|ation d'une page vierge qui va servir de support
    WebBrowser1.Navigate "about:blank"
    WebBrowser1.Document.Open "text/html"
    AjoutFonction = "<html><body>"
    
    '--- image de fond ---
    'AjoutFonction = AjoutFonction & "<BODY background='" & Fichier & "'</BODY>"
 
    '--- texte ---
    Tableau = Split(Message, vbCrLf)
    For i = 0 To UBound(Tableau)
        AjoutFonction = AjoutFonction & Tableau(i) & "<br>"
    Next i
    
    '--- Signature ---
    AjoutFonction = AjoutFonction & "<br>"
    AjoutFonction = AjoutFonction & MailOutlookForm.TextBox5.Value
 
    '--- Fin page ---
    AjoutFonction = AjoutFonction & "</body></html>"
    '---------------------------
 
    'Insertion dans le WebBrowser
    WebBrowser1.Document.writeln AjoutFonction
End Sub
Attribute VB_Name = "ThisWorkbook"
Attribute VB_Base = "0{00020819-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Option Explicit
Private Sub Workbook_Open()
    If RegOpenKey(HKEY_CLASSES_ROOT, "Applications\Outlook.EXE", Cl|fffd|) = 0 Then
        'Outlook.EXE est pr|fffd|sent
        'Fermer la cl|fffd| du registre
        RegCloseKey Cl|fffd|: DoEvents
        'Alors on ouvre outlook
        Set objOutlook = New Outlook.Application
    Else
        MsgBox "Outllok pas installer"
    End If
End Sub

Attribute VB_Name = "xFunction"
Option Explicit
'V|fffd|rifier si Outlook est inastaller
Public Declare PtrSafe Function RegOpenKey Lib "advapi32.dll" Alias "RegOpenKeyA" (ByVal hKey As Long, _
    ByVal lpSubKey As String, phkResult As Long) As Long
Public Declare PtrSafe Function RegCloseKey Lib "advapi32.dll" (ByVal hKey As Long) As Long
Attribute VB_Name = "xMailOutlook"
Option Explicit
'---------------------------------------------------------
'Il faut activer la r|fffd|f|fffd|rence "Microsoft Outlook Library" Avant de lancer cette macro,
' Dans l'|fffd|diteur VBA: Faire Menu / Tools / Reference / Cocher "Microsoft Outlook Library"
'---------------------------------------------------------

Public Sub Envoyer_Mail_Outlook()
Dim Destinataire As String, CopieCacher As String, Objet As String, Corps As String, Signature As String, i As Integer, SupMail As Boolean
Dim BodyHtml As Boolean
Dim Priorite As OlImportance
Dim paragraphe As Variant, Tableau As Variant

On Error GoTo Error_Handler

    Set objOutlook = New Outlook.Application
    Set ObjMail = objOutlook.CreateItem(olMailItem) 'ObjOutlook.CreateItem(0)
    Set Ws1 = ThisWorkbook.Sheets("ConfigMail")
'---------------------------------------------------------
    'Destinataire
    Destinataire = MailOutlookForm.TextBox1.Value
    'Copie cach|fffd|
    CopieCacher = MailOutlookForm.TextBox2.Value
    'L'objet du mail
    Objet = MailOutlookForm.TextBox3.Value
    'Le corp du mail
    Corps = MailOutlookForm.TextBox4.Value
    'Html
    BodyHtml = MailOutlookForm.CheckBox2.Value
    'Signature
    Signature = MailOutlookForm.TextBox5.Value
    'Supprime le mail apr|fffd|s l'envoie
    SupMail = MailOutlookForm.CheckBox1.Value
    'Priorit|fffd|
    If MailOutlookForm.ComboBox1.Value = "Hight" Then Priorite = olImportanceHigh
    If MailOutlookForm.ComboBox1.Value = "Normal" Then Priorite = olImportanceNormal
    If MailOutlookForm.ComboBox1.Value = "Low" Then Priorite = olImportanceLow
'---------------------------------------------------------
    With ObjMail
        'Le destinataire
        .To = Destinataire
        'Copie cach|fffd|
        .BCC = CopieCacher
        '.CC = CopieDestinataire
        'L'objet du mail
        .Subject = Objet
        If MailOutlookForm.ListBox1.ListCount > 0 Then 'Avec pi|fffd|ce jointe
            For i = 0 To MailOutlookForm.ListBox1.ListCount - 1
                .Attachments.Add MailOutlookForm.ListBox1.List(i, 0) '.Attachments.Add "C:\Data\essai.txt"
            Next i
        End If
        'Le corps du mail ..son contenu
        'En text brut
        If BodyHtml = False Then
            .BodyFormat = olFormatRichText
            .Body = Corps & vbCrLf & vbCrLf & Signature
        'En html
        Else
            .BodyFormat = olFormatHTML
            'mise en forme du paragraphe
            paragraphe = "<html><body>"
            Tableau = Split(Corps, vbCrLf)  'coupe la chaine du textbox par les retour |fffd| la ligne
            'boucle sur le tableau pour visualiser le r|fffd|sultat
            For i = 0 To UBound(Tableau)
                paragraphe = paragraphe & Tableau(i) & "<br>"
            Next i
            paragraphe = paragraphe & "<br>"
            paragraphe = paragraphe & Signature
            paragraphe = paragraphe & "</body></html>"
            'fin mise en forme du paragraphe
            .HTMLBody = paragraphe
        End If
        'Priorit|fffd|
        .Importance = Priorite
        'Permet de supprimer la mail apr|fffd|s l'envoie
        .DeleteAfterSubmit = SupMail
        '.Save 'Sauvegarder le mail avant l'envoie
        '.Display  'Ici on peut supprimer pour l'envoyer sans v|fffd|rification
        .Send
    End With
    MsgBox "Mail Envoyer"
    objOutlook.Quit
    
fin:
    Set ObjMail = Nothing
    Set objOutlook = Nothing
    Set Ws1 = Nothing
    Exit Sub
    
Error_Handler:
    MsgBox "MS Excel a g|fffd|n|fffd|r|fffd| l'erreur suivante :" & vbCrLf & vbCrLf & _
    "Num|fffd|ro d'erreur : " & Err.Number & vbCrLf & _
    "Source d'erreur : EnvoieMail" & vbCrLf & _
    "Description de l'erreur : " & Err.Description, vbCritical, "Une erreur s'est produite!"
    Resume fin
End Sub

'Actions , propri|fffd|t|fffd|
'Application , propri|fffd|t|fffd|
'Attachments , propri|fffd|t|fffd|
'AutoResolvedWinner , propri|fffd|t|fffd|
'BillingInformation , propri|fffd|t|fffd|
'Body , propri|fffd|t|fffd|
'BodyFormat , propri|fffd|t|fffd|
'Categories , propri|fffd|t|fffd|
'Class , propri|fffd|t|fffd|
'Companies , propri|fffd|t|fffd|
'Conflicts , propri|fffd|t|fffd|
'ConversationIndex , propri|fffd|t|fffd|
'ConversationTopic , propri|fffd|t|fffd|
'CreationTime , propri|fffd|t|fffd|
'DownloadState , propri|fffd|t|fffd|
'EntryID , propri|fffd|t|fffd|
'ExpiryTime , propri|fffd|t|fffd|
'FormDescription , propri|fffd|t|fffd|
'GetInspector , propri|fffd|t|fffd|
'HTMLBody , propri|fffd|t|fffd|
'Importance , propri|fffd|t|fffd|
'InternetCodepage , propri|fffd|t|fffd|
'IsConflict , propri|fffd|t|fffd|
'ItemProperties , propri|fffd|t|fffd|
'LastModificationTime , propri|fffd|t|fffd|
'Links , propri|fffd|t|fffd|
'MarkForDownload , propri|fffd|t|fffd|
'MessageClass , propri|fffd|t|fffd|
'Mileage , propri|fffd|t|fffd|
'NoAging , propri|fffd|t|fffd|
'OutlookInternalVersion , propri|fffd|t|fffd|
'OutlookVersion , propri|fffd|t|fffd|
'Parent , propri|fffd|t|fffd|
'ReceivedTime , propri|fffd|t|fffd|
'Saved , propri|fffd|t|fffd|
'SenderEmailAddress , propri|fffd|t|fffd|
'SenderEmailType , propri|fffd|t|fffd|
'SenderName , propri|fffd|t|fffd|
'Sensitivity , propri|fffd|t|fffd|
'SentOn , propri|fffd|t|fffd|
'Session , propri|fffd|t|fffd|
'Size , propri|fffd|t|fffd|
'Subject , propri|fffd|t|fffd|
'UnRead , propri|fffd|t|fffd|
'UserProperties , propri|fffd|t|fffd|
Attribute VB_Name = "xVarPublic"
Option Explicit
'---------------------------------------------------------
'D|fffd|claration des variables public
'---------------------------------------------------------
'Outlook
'---------------------------------------------------------
Public objOutlook As New Outlook.Application
Public ObjNameSpace As Namespace
Public ObjMail As Outlook.MailItem
'---------------------------------------------------------
'
'Outlook installer
Public Const HKEY_CLASSES_ROOT = &H80000000
Public Cl|fffd| As Long
'
'WorkSheet
Public Ws1 As Worksheet
Public Ws2 As Worksheet
'
'Mail
Public bActiver As Boolean



INQUEST-PP=macro
