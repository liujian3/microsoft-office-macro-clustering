Attribute VB_Name = "MallProc"

Option Explicit

Sub Mall_Specifik_Procedur()
Attribute Mall_Specifik_Procedur.VB_ProcData.VB_Invoke_Func = " \n14"
    
    Range("A1").Select
          
End Sub
Attribute VB_Name = "Sheet1"
Attribute VB_Base = "0{00020820-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Attribute VB_Name = "StartProc"
'   PAPLUS32.XLTM
'   Orginalmall, Anv|fffd|nds av makrot i Paplus32.xla som "mall" n|fffd|r ny mall skapas.
'--------------------------------------------------------------------------------
'   20020215 Omgjord f|fffd|r att klara mallar i b|fffd|de Windows och Webb milj|fffd|
'   20020802 Formaterar numeriskt v|fffd|rde >11 siffror till tal
'   20030909 Anropar: MallSpecifikProcedur om startr. Webb
'   20050627  Certifikat i denna version
'   20080508 |fffd|ndrad f|fffd|r 2007 av KH
'   20081030 |fffd|ndrad special, kopiera iniblad, f|fffd|r att klara Office 2007 BE
'   20090112 St|fffd|nger datafil med SaveChanges:=False f|fffd|r att slippa fr|fffd|ga om spara BE
'   20090223 All kod genomg|fffd|ngen och f|fffd|ljande genomf|fffd|rt:
'           * Redundant kod tas bort.
'           * Ny koddokumentation inf|fffd|rd
'           * Metodutbrytning och konstantdefiniering /KH
'   20091015 Mallexport via SSO
'   20100209 BEEJ Tagit bort kod i objektet "ThisWorkbook" testkod?
'   20100928 HENI fixat problem med kolumntyper i export till egen mall
'   20110323 HENI fixat problem med infogning av rader n|fffd|r "f|fffd|rsta" raden ska kopieras f|fffd|r efterf|fffd|ljande rader.
'   20110909 HENI ing|fffd|r i release f|fffd|r 6.20
'   20121129 HENI ing|fffd|r i release f|fffd|r 6.30.01
'            * Ny infognings hantering.
'            * Kolumn formateringen ger nu som defaultv|fffd|rde "generel" ist|fffd|llet f|fffd|r "@" (text), samt omstrukturering av den funktionen.
'---------------------------------------------------------------------------
Option Explicit

Dim mstrTypeWEB As String
Dim mintNrOfIdsWEB As Integer
Dim mstrDatafileWEB As String
Dim gstrColumnType As String
Dim gstrColumnTypeForSpecialTemplate As String
Dim intHighestColumnNumber As Integer
'Dim mstrGenerationWEB As String ' HENI 20091015 - Mallexport via sso

Const mkstrGeneration60020 As String = "G" & "E" & "N" & "E" & "R" & "A" & "T" & "I" & "O" & "N" & "_" & "6" & "." & "0" & "0" & "." & "2" & "0" & "_" & "W" & "E" & "B" & "B" ' HENI 20091015 - Mallexport via sso

Sub Auto_Open()
    
    ' K|fffd|rs automatiskt vid |fffd|ppning.
    
    If SheetExists_WEB(ActiveWorkbook.Name, "Iniblad") Then
        
        ' Om ini-bladet existerar, h|fffd|mtas Variabeln p|fffd| rad 21, kolumn 2.
        mstrTypeWEB = ActiveWorkbook.Worksheets("Iniblad").Cells(21, 2)
        
        ' K|fffd|r procedur
        STARTRUTIN
    
    End If
    
End Sub

Sub STARTRUTIN()
    
    Const Str_WINDOW As String = "S" & "T" & "A" & "R" & "T" & "R" & "U" & "T" & "I" & "N" & "_" & "W" & "I" & "N" & "D" & "O" & "W"
    Const Str_WEBBEN As String = "S" & "T" & "A" & "R" & "T" & "R" & "U" & "T" & "I" & "N" & "_" & "W" & "E" & "B" & "B" & "E" & "N"
        
    ' K|fffd|r respektive start-rutin beroende p|fffd| variabeln fr|fffd|n Ini-bladet. (se. Auto_Open).
        
    If mstrTypeWEB = Str_WINDOW Then WINDOW_START
    If mstrTypeWEB = Str_WEBBEN Then WEBBEN_START

End Sub

Sub WINDOW_START()
    
On Error Resume Next

    ' L|fffd|gger till h|fffd|ndelsen: "L|fffd|gg_Upp_PaMeny" att automatiskt k|fffd|ras vid "OnWindow" h|fffd|ndelsen.
    ' Dvs. Denna k|fffd|rs: "whenever you activate a window".
    Windows(ActiveWorkbook.Name).OnWindow = "L|fffd|gg_Upp_PaMeny"
    
    ' K|fffd|r denna funktion forcerat
    L|fffd|gg_Upp_PaMeny
    
    ' Kontrollerar om stora tal finns bland data.
    Koll_Stora_Tal
    
    ' Om inst|fffd|llningen fr|fffd|n Ini-bladet |fffd|r: "MOTEGENMALL"
    If Worksheets("Iniblad").Cells(13, 2).Value = "MOTEGENMALL" Then Mall_Specifik_Procedur
    
    
End Sub

Sub WEBBEN_START()

On Error Resume Next

    ' Inaktiverar sk|fffd|rmuppdateringen.
    ScreenOff_WEB
        
    ' H|fffd|mtar inst|fffd|llning fr|fffd|n Ini-bladet
    mstrDatafileWEB = Trim(ThisWorkbook.Worksheets("Iniblad").Cells(22, 2))
    
    ' HENI 20091015 - Mallexport via sso
    ' Kollar ifall det |fffd|r en xlsm fil som |fffd|r skickad, = SSO l|fffd|sning med inb|fffd|ddad fil
    If LCase(Mid(ThisWorkbook.Name, InStrRev(ThisWorkbook.Name, ".") + 1)) = "xlsm" Then
        
        Webbdata_Excel_2007
          
    End If
    ' HENI 20091015 - Mallexport via sso
    
    ' Koppling mot mall startas.
    Koppla_Mot_Mall_WEB
    
    ' Kontrollerar stora tal
    Koll_Stora_Tal
    
    ' Procedur f|fffd|r olika malltyper
    Mall_Specifik_Procedur
    
    ' Om det har skapats en txt fil i temp s|fffd| skall den plockas bort
    If Trim(ThisWorkbook.Worksheets("Iniblad").Cells(23, 2)) = mkstrGeneration60020 Then
    
        Ta_Bort_Fil (mstrDatafileWEB)
    
    End If
    
    ' Rensar bort Ini-bladet.
    Ta_Bort_Blad_WEB ActiveWorkbook.Name, "Iniblad"
    
    ' Rensar kopieringsl|fffd|get (tar bort blinkande ram runt urklippt objekt)
    Application.CutCopyMode = False
    
    ' HENI 20091015 - Mallexport via sso
    ' Rensar bort Parameter-bladet
    Ta_Bort_Blad_WEB ActiveWorkbook.Name, "Parametrar"
    
    ' Rensar bort Orginalblad-bladet
    Ta_Bort_Blad_WEB ActiveWorkbook.Name, "Orginalblad"
    
    ' Skapa en ny osparad arbetsbok med den relevanta informationen fr|fffd|n denna mallen
    Worksheets.Copy
    ' HENI 20091015 - Mallexport via sso
    
    ' Aktiverar sk|fffd|rmuppdateringen.
    ScreenOn_WEB
    
    ' St|fffd|nger denna arbetsbok
    ThisWorkbook.Close False ' HENI 20091015 - Mallexport via sso
    
End Sub

Sub L|fffd|gg_Upp_PaMeny()
    
    ' Rensar eventuell PA-meny, l|fffd|gger sedan in den igen.
   
On Error GoTo errHandler
   
    ' Rensar f|fffd|rst bort menyn om den redan finns.
    Ta_Bort_PaMeny
    
    ' K|fffd|r funktionen "Inst_Menyer", finns i Paplus32.xla, denna k|fffd|rs utan referens.
    Application.Run "Inst_Menyer"
    
    ' Byter h|fffd|ndelsen vid "OnWindow" till "Ta_Bort_PaMeny".
    ' Dvs. Denna k|fffd|rs: "whenever you activate a window".
    Application.OnWindow = "Ta_Bort_PaMeny"
        
    Exit Sub

errHandler:
    ' Vid eventuellt fel s|fffd| avslutas kodk|fffd|rningen.
    End
      
End Sub

Sub Ta_Bort_PaMeny()
    
    ' L|fffd|gger tillbaka "Save" och "Save as..." om dessa fanns innan vi startade.
    ' Samt rensar HR-Plus menyn.
    
On Error Resume Next

    If Application.Version < 12 Then
        If Application.Run("ReturnbolSaveAsExisted") Then Application.CommandBars("File").Controls.Add 1, 748, , 5
        If Application.Run("ReturnbolSaveExisted") Then Application.CommandBars("File").Controls.Add 1, 3, , 4
    End If
    
    MenuBars(xlWorksheet).Menus("HR-Plus").Delete
    MenuBars(xlChart).Menus("HR-Plus").Delete

End Sub
    
Private Sub Koll_Stora_Tal()

    ' G|fffd|r igenom alla kolumner i filen som har en rubrik.
    ' Om inneh|fffd|llet anses vara ett "stort tal" s|fffd| s|fffd|tts formatet p|fffd| cellerna till NumberFormat = "0".

    Dim intColumn As Integer
    Dim varTemp As Variant
    Dim bolDone As Boolean
    
    intColumn = 1

    Do While Not bolDone
        
        If Cells(1, intColumn) = "" Then
            bolDone = True
        Else
        
            varTemp = Cells(2, intColumn)
            
            If IsNumeric(varTemp) Then
            
                If varTemp > 100000000000# Then
                    Columns(intColumn).Select
                    Selection.NumberFormat = "0"
                End If
                
            End If
            
        End If
        
        intColumn = intColumn + 1
        
    Loop
  
    Range("A1").Select

End Sub

Sub Auto_Close()
    
    ' K|fffd|rs automatiskt vid st|fffd|ngning. Rensar h|fffd|ndelsen vid "OnWindow" och st|fffd|nger sedan
    ' arbetsboken antingen med spara eller utan.
    
    ' Meddelandetext till anv|fffd|ndaren s|fffd|tts.
    Dim strMessage As String
    strMessage = "Save Changes in " & ActiveWorkbook.Name
    
    If Not ActiveWorkbook.Saved Then
    
        ' Om arbetsboken har |fffd|ndringar som ej |fffd|r sparade
    
        ' Ger anv|fffd|ndaren dialogruta med valet om: SPARA, SPARA INTE, AVBRYT.
        Select Case MsgBox(prompt:=strMessage, Buttons:=vbYesNoCancel, Title:="HR-plus EXCEL")
            Case vbYes
                
                ' SPARA
                If mstrTypeWEB <> "STARTRUTIN_WEBBEN" Then Windows(ActiveWorkbook.Name).OnWindow = ""
                ActiveWorkbook.Close SaveChanges:=True
            
            Case vbNo
                
                ' SPARA INTE
                If mstrTypeWEB <> "STARTRUTIN_WEBBEN" Then Windows(ActiveWorkbook.Name).OnWindow = ""
                ActiveWorkbook.Close SaveChanges:=False
            
            Case Else
            
                ' AVBRYT
                Exit Sub
                
        End Select
        
    Else
        
        ' Arbetsboken har inga osparade f|fffd|r|fffd|ndringar.
        If mstrTypeWEB <> "STARTRUTIN_WEBBEN" Then Windows(ActiveWorkbook.Name).OnWindow = ""
        ActiveWorkbook.Close
        
    End If
    
    If mstrTypeWEB <> "STARTRUTIN_WEBBEN" Then Application.OnWindow = ""
    
    ' Rensa bort PA-menyn.
    Ta_Bort_PaMeny
   
End Sub
    
Sub Koppla_Mot_Mall_WEB()

    Dim strTemplateWorkbook As String
    Dim strDataWorkbook As String
    Dim intNrOfSheets As Integer
    Dim wrkWorksheet As Worksheet
    
On Error Resume Next

    ' Inaktiverar sk|fffd|rmuppdatering och s|fffd|tter meddelande i statusf|fffd|ltet.
    ScreenOff_WEB "Koppling mot mall p|fffd|g|fffd|r, var god dr|fffd|j ..."
    
    ' Sparar undan aktiv arbetsboksnamn
    strTemplateWorkbook = ActiveWorkbook.Name
    
    ' Endast mallar har Parameterblad. Annars Utan mall.
    If SheetExists_WEB(strTemplateWorkbook, "Parametrar") Then Workbooks(strTemplateWorkbook).Worksheets("Parametrar").Visible = True
    
    ' H|fffd|mta information om kolumntyper fr|fffd|n inibladet
    gstrColumnType = Trim(ThisWorkbook.Worksheets("Iniblad").Cells(18, 2))
    gstrColumnTypeForSpecialTemplate = String(Len(gstrColumnType) + 100, "0")
    
    ' Om bladet "Orginalblad" finns, s|fffd| tas detta bort.
    Ta_Bort_Blad_WEB strTemplateWorkbook, "Orginalblad"
    
    If SheetExists_WEB(strTemplateWorkbook, "Iniblad") Then
            
        ' Om Ini-bladet finns, s|fffd| h|fffd|mtas inst|fffd|llning fr|fffd|n densamma
        On Error Resume Next
        
        mintNrOfIdsWEB = CInt(Workbooks(strTemplateWorkbook).Worksheets("Iniblad").Cells(17, 2))
        If Err.Number <> 0 Then mintNrOfIdsWEB = 1
        Err.Clear
        
        ' start(1 av 2) 0810 BEEJ
        ' ---------------
        ' Skapa nytt Orginalblad genom att kopiera Inibladet. Klarar alla versioner?
        With Workbooks(strTemplateWorkbook).Worksheets("Iniblad")
            .Visible = True
            .Select
            .Copy After:=Workbooks(strTemplateWorkbook).Worksheets("Iniblad")
        End With
        
        ' Byter namn p|fffd| det nya bladet.
        Workbooks(strTemplateWorkbook).Worksheets("Iniblad (2)").Name = "Orginalblad"
        
        ' Rensar det nya bladet (finns bara info p|fffd| rad 1-36 i ini-bladet.)
        With Workbooks(strTemplateWorkbook).Worksheets("Orginalblad")
            .Select
            .Rows("1:36").Select
        End With
        Selection.Delete Shift:=xlUp
        ' ---------------
        ' slut(1 av 2) 0810 BEEJ
    
    Else
    
        ' Om Ini-bladet saknas, s|fffd| avbryts kopplingen.
        ' Sk|fffd|rmuppdatering |fffd|teraktiveras, felmeddelande ges och sedan avslutas k|fffd|rningen.
        ScreenOn_WEB
        MsgBox ("Connection or Template is not working!")
        Exit Sub
    End If
    
    ' Tilldelar "Orginalbladet" till variabeln wrkWorksheet
    Set wrkWorksheet = Workbooks(strTemplateWorkbook).Worksheets("Orginalblad")
    
    ' start(2 av 2) 0810 BEEJ
    ' ---------------
    If Len(gstrColumnType) > 0 Then
        Workbooks.OpenText mstrDatafileWEB, , , xlDelimited, , , True, , , , , , KolTypArr(gstrColumnType)
    Else
        Workbooks.OpenText mstrDatafileWEB, , , xlDelimited, , , True
    End If
    
    strDataWorkbook = ActiveWorkbook.Name
    
    With Workbooks(strDataWorkbook)
        .Sheets(1).Select
        .Sheets(1).Name = "Data_Webb"
    
        Columns.EntireColumn.AutoFit
    
        .Sheets(1).Range("A1").Select
        .Sheets(1).Range(Selection, Selection.End(xlDown)).Select      ' markera o kopiera all data
        .Sheets(1).Range(Selection, Selection.End(xlToRight)).Select
    End With
    Selection.Copy
    
    With Workbooks(strTemplateWorkbook)
        
        .Sheets("Orginalblad").Activate
        .Sheets("Orginalblad").Range("A1").Select
        .Sheets("Orginalblad").Paste
    
        Application.CutCopyMode = False
   
        Workbooks(strDataWorkbook).Activate                           ' tag bort Databok
        Workbooks(strDataWorkbook).Close SaveChanges:=False               ' 20090112 BE
        
        .Activate
        ' ---------------
        ' slut(2 av 2) 0810 BEEJ

        wrkWorksheet.Select
           
        ' Flytta inl|fffd|st data till databladet
        
        If SheetExists_WEB(strTemplateWorkbook, "Parametrar") Then
            
            ' Om mall. Flytta enligt parameterblad.
            wrkWorksheet.Move , ActiveWorkbook.Worksheets("Parametrar")
            Till_Kolumn_WEB
            .Worksheets("Parametrar").Visible = False
            .Worksheets("Orginalblad").Visible = False
        Else
            
            Ta_Bort_Blad_WEB strTemplateWorkbook, "Data"
            
            wrkWorksheet.Name = "Data"
        
        End If
        
        ' HENI 20091015 - D|fffd|ljer Ini-bladet, annars |fffd|r det Ini-bladet som f|fffd|r auto justerade kolumner
        .Worksheets("Iniblad").Visible = False
        
    End With
    
    ' Formatera kolumner endast d|fffd| det sker koppling mot mall.
    If SheetExists_WEB(strTemplateWorkbook, "Parametrar") Then
        FormateraKolumn gstrColumnTypeForSpecialTemplate
    End If
    
    Columns.EntireColumn.AutoFit
    
    ' Aktiverar sk|fffd|rmuppdateringen
    'ScreenOn_WEB
    
End Sub

Sub Till_Kolumn_WEB()
    
    Dim intNrOfRows As Integer
    Dim bolInsert As Boolean
    Dim strTemplateColumn As String
    Dim intParameterColumn As Integer
    Dim intRow As Integer
    Dim intOriginalColumn As Integer
    Dim intTemplateRow As Integer
    Dim intOriginalTemplateRow As Integer
    
    ' Startv|fffd|rde f|fffd|r antal rader
    intNrOfRows = 1
    
    With ActiveWorkbook
        
        ' Om INFOGA finns i parametrar-bladet
        bolInsert = (Trim(.Worksheets("Parametrar").Cells(2, 2)) = "INFOGA")
        
        ' R|fffd|kna antalet rader.
        Do Until Trim(.Worksheets("Orginalblad").Cells(intNrOfRows, 1)) = ""
            intNrOfRows = intNrOfRows + 1
        Loop
        ' Loopen g|fffd|r ett steg f|fffd|r l|fffd|ngt, justera f|fffd|r detta:
        intNrOfRows = intNrOfRows - 1
        
        ' Startv|fffd|rden f|fffd|r kolumner.
        strTemplateColumn = "A"
        intParameterColumn = 1
        
        ' Loopar tills kolumnen |fffd|r tom, annars kopierar den rad f|fffd|r rad, kolumnvis.
        Do Until Trim(.Worksheets("Parametrar").Cells(1, intParameterColumn)) = ""
            
            strTemplateColumn = .Worksheets("Parametrar").Cells(3, intParameterColumn)
            
            If Trim(strTemplateColumn) > "0" Then
            
                ' spara undan kolumntyp information
                KeepType "Add", strTemplateColumn, intParameterColumn
                
                intRow = 2
                intOriginalColumn = intParameterColumn - mintNrOfIdsWEB
                intTemplateRow = .Worksheets("Parametrar").Cells(4, intParameterColumn)
                intOriginalTemplateRow = intTemplateRow
                
                Do While intRow <= intNrOfRows
                    
                    If bolInsert Then
                        
                        ' K|fffd|rs enbart f|fffd|rsta g|fffd|ngen i den yttre kolumnloopen. (kopierar in rubriker)
                        
                        If intOriginalTemplateRow <> intTemplateRow Then
                            .Worksheets(1).Cells(intTemplateRow, strTemplateColumn).Select
                            Selection.EntireRow.Insert
                        End If
                        
                        .Worksheets(1).Select
                        
                        If Trim(.Worksheets("Parametrar").Cells(2, 4)) <> "" Then
                            ' HENI 20121129 - |fffd|ndra s|fffd| att man inte infogar rubrik raden och f|fffd|ljer dess formatering.
                            .Worksheets(1).Rows(intTemplateRow).Select
                        Else
                            .Worksheets(1).Rows(intTemplateRow - 1).Select
                        End If
                        Selection.Copy
                        
                        .Worksheets(1).Cells(intTemplateRow, 1).Select
                        ActiveSheet.Paste
                        
                        Application.CutCopyMode = False
                    
                    End If
                    
                    .Worksheets(1).Cells(intTemplateRow, strTemplateColumn) = .Worksheets("Orginalblad").Cells(intRow, intOriginalColumn)
                    
                    ' N|fffd|sta mallrad
                    intTemplateRow = intTemplateRow + 1
                    
                    ' N|fffd|sta rad
                    intRow = intRow + 1
                    
                    ' Koll p|fffd| hur m|fffd|nga kolumner som populeras
                    If intHighestColumnNumber < Range(strTemplateColumn & "1").Column Then
                        intHighestColumnNumber = Range(strTemplateColumn & "1").Column
                    End If
                    
                Loop
                
                bolInsert = False
            End If
            
            ' N|fffd|sta kolumn.
            intParameterColumn = intParameterColumn + 1
        Loop
        
    End With
    
    ' G|fffd|r kolumntyp str|fffd|ngen till korrekt l|fffd|ngd
    KeepType "Trim", "", intHighestColumnNumber
    
End Sub

Function SheetExists_WEB(strBookName As String, strSheetName As String) As Boolean
               
    ' Loopar igenom arbetsboken och j|fffd|mf|fffd|r namnen p|fffd| bladen.
              
    Dim intCounter As Integer
    
    For intCounter = 1 To Workbooks(strBookName).Sheets.Count
        If Workbooks(strBookName).Sheets(intCounter).Name = strSheetName Then
            SheetExists_WEB = True
            Exit Function
        End If
    Next
    
    SheetExists_WEB = False
   
End Function

Function Ta_Bort_Blad_WEB(strBook As String, strSheet As String)

    ' Tar bort bladet strSheet, i boken: strBook.

On Error Resume Next
    
    Application.DisplayAlerts = False
    Workbooks(strBook).Worksheets(strSheet).Visible = True
    Workbooks(strBook).Worksheets(strSheet).Delete
    Application.DisplayAlerts = True
    
End Function

Private Sub ScreenOff_WEB(Optional strMessage As String = "Wait...")

    Application.StatusBar = strMessage
    Application.ScreenUpdating = False
    
End Sub

Private Sub ScreenOn_WEB()

    Application.StatusBar = False
    Application.ScreenUpdating = True
    
End Sub

Private Sub Kopiera_Sig_Sjalv_Till_Zip(strWorkplace As String)

    ' Lokala variabler
    Dim varCopy As Variant
    Dim strPathToThisWorkbook As String
    Dim strPathToCopy As String
    Dim strZipFileName As String
    
    ' Skapar zip filens namn basserat p|fffd| mallens namn
    strZipFileName = Left(ThisWorkbook.Name, Len(ThisWorkbook.Name) - 4) & "zip"
    
    ' Skapar s|fffd|kv|fffd|gen till denna mallen
    strPathToThisWorkbook = ThisWorkbook.FullName
    
    ' Skapar s|fffd|kv|fffd|gen till kopian som sparas som en zip fil
    strPathToCopy = strWorkplace & strZipFileName
    
    ' Kopierar denna mallen till zip kopian
    Set varCopy = CreateObject("Scripting.FileSystemObject")
    varCopy.CopyFile strPathToThisWorkbook, strPathToCopy
    
    ' Ta bort l|fffd|nken till den nya filen
    Set varCopy = Nothing

End Sub

Private Sub Extrahera_Xml(strWorkplace As String)
    
    ' Lokala variabler
    Dim objSc As Object
    Dim varZipFile As Variant
    Dim varDestinationFolder As Variant
    Dim varItems As Variant
    
    ' Anv|fffd|nder Systemets egna f|fffd|rm|fffd|ga att |fffd|ppna upp zip filer
    Set objSc = CreateObject("Shell.Application")
    
    ' S|fffd|kv|fffd|gen till zip filen
    Set varZipFile = objSc.Namespace(strWorkplace & _
        Left(ThisWorkbook.Name, Len(ThisWorkbook.Name) - 4) & "zip")

    ' |fffd|nskad plats d|fffd|r den extraherade filen kommer att l|fffd|ggas, f|fffd|rslagsvis %TEMP%
    Set varDestinationFolder = objSc.Namespace(Left(strWorkplace, InStrRev(strWorkplace, "\") - 1))
    
    ' Plocka ut den |fffd|nskade filen
    Set varItems = varZipFile.Items.Item(mstrDatafileWEB & "")
    
    ' Placera filen p|fffd| |fffd|nskad plats
    varDestinationFolder.CopyHere varItems
    
    ' G|fffd|r om xml filen till en txt fil f|fffd|r att inte inte excel ska behandla filen som en xml fil
    Name strWorkplace & mstrDatafileWEB As _
        strWorkplace & Left(mstrDatafileWEB, InStrRev(mstrDatafileWEB, ".")) & "txt"
    
    'MsgBox "-Testning av makro-" & vbCrLf & _
    '       "SSO Aktiverat makro k|fffd|rs." & vbCrLf & _
    '       "Arbetsmapp som anv|fffd|nds: " & strWorkplace & vbCrLf & _
    '       "Xml filens namn: " & mstrDatafileWEB & vbCrLf & _
    '       "Generation: " & ThisWorkbook.Worksheets("Iniblad").Cells(23, 2) & vbCrLf & vbCrLf & _
    '       "Tryck p|fffd| OK f|fffd|r att forts|fffd|tta."
    
    ' Tilldelar variabeln mstrDatafileWEB med hela s|fffd|kv|fffd|gen till den extraherade filen
    mstrDatafileWEB = strWorkplace & Left(mstrDatafileWEB, InStrRev(mstrDatafileWEB, ".")) & "txt"
    
    ' Ta bort Zip filen som vi inte l|fffd|ngre beh|fffd|ver
    Ta_Bort_Fil (strWorkplace & _
        Left(ActiveWorkbook.Name, Len(ActiveWorkbook.Name) - 4) & "zip")
    
End Sub

Private Sub Ta_Bort_Fil(fileToDelete As String)

    ' Tar bort o|fffd|nskade filer, i detta fallet v|fffd|ra temp filer.
    ' Som parameter anv|fffd|nds s|fffd|kv|fffd|gen till filen inklusive filens namn
    
    ' Kontrollera att filen existerar
    If Len(Dir$(fileToDelete)) > 0 Then

        ' Ifall filen |fffd|r skyddad fr|fffd|n att tas bort s|fffd| |fffd|ndras det attributet h|fffd|r
        SetAttr fileToDelete, vbNormal
        
        ' Borttagningnen av filen
        Kill (fileToDelete)
    
    End If

End Sub

Private Sub Webbdata_Excel_2007()

    ' Kollar vilken mall Generation som anv|fffd|nds
    If Trim(ThisWorkbook.Worksheets("Iniblad").Cells(23, 2)) = mkstrGeneration60020 Then
        
        ' F|fffd|r|fffd|ndra URL l|fffd|nken till en xml fil som best|fffd|r av den unika nummerserien som finns i URL:en
        mstrDatafileWEB = Mid(mstrDatafileWEB, InStrRev(mstrDatafileWEB, "=") + 1) & ".xml"
            
        ' Skapa en zip kopia av sig sj|fffd|lv
        Kopiera_Sig_Sjalv_Till_Zip (Environ("TEMP") & "\")
            
        ' Plocka ut den inb|fffd|ddade filen ur det skapade zip arkivet
        Extrahera_Xml (Environ("TEMP") & "\")
    
    End If

End Sub

Sub FormateraKolumn(strValue As String)
   
    ' S|fffd|ker igenom strValue med kolumntyper ( Ex "CCNCD"),
    ' S|fffd|tter Format p|fffd| Text och Datumkolumner

    Dim intCounter As Integer
    
    For intCounter = 1 To Len(strValue)
        With ActiveSheet.Columns(intCounter)
            Select Case Mid(strValue, intCounter, 1)
                Case "N"
                    ' G|fffd|r ingenting
                Case "0"
                    ' G|fffd|r ingenting
                Case "D"
                    .NumberFormat = "YYYY-MM-DD"
                Case Else
                    .NumberFormat = "General"
            End Select
            .Select
        End With
    Next
    
    Selection.End(xlToLeft).Select

End Sub

Sub KeepType(actionType As String, col As String, pinPoint As Integer)
    
    If actionType = "Add" Then
        ' HENI 20100927 - H|fffd|ller reda p|fffd| vilken kolumntyp som ska anv|fffd|ndas.
        Dim startString As String
        
        startString = Left(gstrColumnTypeForSpecialTemplate, Range(col & "1").Column - 1)
        gstrColumnTypeForSpecialTemplate = Replace(gstrColumnTypeForSpecialTemplate, "0", Mid(gstrColumnType, pinPoint, 1), Range(col & "1").Column, 1)
        gstrColumnTypeForSpecialTemplate = startString & gstrColumnTypeForSpecialTemplate
    Else
        gstrColumnTypeForSpecialTemplate = Left(gstrColumnTypeForSpecialTemplate, pinPoint)
    End If
    
End Sub

Function KolTypArr(strValue As String) As Variant
   
    ' G|fffd|r igenom strValue, och returnerar en Array med datatyp f|fffd|r varje bokstav.
   
    Dim varReturn() As Variant
    Dim intCounter As Integer
    
    ReDim varReturn(1 To Len(strValue))
    intCounter = 1
   
    For intCounter = 1 To Len(strValue)
      
        Select Case Mid(strValue, intCounter, 1)
            Case "N"
                ' Allm|fffd|n (=Numerisk om siffror)
                varReturn(intCounter) = Array(intCounter, 1)
            Case "D"
                ' Datum |fffd|MD
                varReturn(intCounter) = Array(intCounter, 5)
            Case Else
                ' Text
                varReturn(intCounter) = Array(intCounter, 2)
        End Select
    Next
      
    KolTypArr = varReturn
      
End Function





Attribute VB_Name = "ThisWorkbook"
Attribute VB_Base = "0{00020819-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Sub testa()

    Worksheets("Iniblad").Visible = True

End Sub



' InQuest injected base64 decoded content
' *^r&
' Szjej

INQUEST-PP=macro
