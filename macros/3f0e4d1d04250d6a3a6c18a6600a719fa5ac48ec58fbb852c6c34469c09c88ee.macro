Attribute VB_Name = "Common"
' WORD 97 MINI-WIZARD  -  Common VBA Module
' --------------------------------------------------------------------------------------
Option Explicit

' --------------------------------------------------------------------------------------
' CONSTANT DECLARATIONS

' Error messages
Public Const strERR_BAD_DOC         As String = "Der Assistent kann nicht im aktiven Dokument verwendet werden. Um den Assistenten zu verwenden, w|fffd|hlen Sie ""Neu"" im Men|fffd| ""Datei"" und w|fffd|hlen Sie den "
Public Const strERR_BAD_DOC2        As String = " aus."

' --------------------------------------------------------------------------------------
' ************     DO NOT LOCALIZE ANY CODE BEYOND THIS POINT  *************************
' --------------------------------------------------------------------------------------

' PUBLIC DECLARATIONS
Public fBalloonChoice           As Long
Public fAssistantAvailable      As Boolean
Public objHelpTipBalloon        As Balloon

Public Sub StartWizard()

    InitWizardStrings
    InitDoc
    System.Cursor = wdCursorNormal
    Application.ScreenRefresh
    InitiateWizard

End Sub ' StartWizard

Private Sub InitDoc()
    Dim fBadDoc         As Integer
    Dim objActiveRange  As Object
    
    On Error GoTo FatalError

    ' Check if doc.path errors if path not present.
    fBadDoc = False
    If (Len(ActiveDocument.Path) <> 0) Then
        'doc has been saved before
        fBadDoc = True
    Else        'Remove this Else clause for the Letter Wizard
        Set objActiveRange = ActiveDocument.Content
        ' Check if range has anything other than a paragraph mark
        If (objActiveRange.End > objActiveRange.Start + 1) Then fBadDoc = True
    End If
    
    If (fBadDoc) Then
        DisplayErrorMsg strERR_BAD_DOC & strWIZ_NAME & strERR_BAD_DOC2
        ActiveDocument.Close wdDoNotSaveChanges
        End ' Terminate wizard
    End If
    
    WordBasic.FileSummaryInfo Title:=""     'Need to use WB to zero this
    Exit Sub

FatalError:
    ReportError Err

End Sub  ' InitDoc


Public Sub InitiateWizard()
    Dim i                   As Integer
    Dim objInitialBalloon   As Object

    On Error GoTo ErrorInitBalloon
    
    ' Create new Assistant Balloon
    Set objInitialBalloon = Assistant.NewBalloon
    With objInitialBalloon
        .Heading = strBALLOON_HEADING
        .Button = msoButtonSetCancel
        .BalloonType = msoBalloonTypeButtons
    End With

    ' Define Balloon Text
    With objInitialBalloon.Labels
        .Count = iBALLOON_OPTIONS
        For i = 1 To iBALLOON_OPTIONS
            .Item(i).Text = strInitialBalloon(i - 1)
        Next i
    End With

    ' Show Balloon and get user's choice
    fBalloonChoice = objInitialBalloon.Show
    fAssistantAvailable = True
    
    ' Service choice
    On Error GoTo WizardError
    fnInitialBalloon (fBalloonChoice)
    Exit Sub
    
ErrorInitBalloon:
    ' Use Dialog Form
    fAssistantAvailable = False
     
    On Error GoTo WizardError
    
    ' Initialize form labels
    formOptions.Caption = strWIZ_NAME
    formOptions.lblPrompt.Caption = strBALLOON_HEADING
    formOptions.optOne.Caption = strInitialBalloon(0)
    formOptions.optTwo.Caption = strInitialBalloon(1)

    ' Set localizable accelerator keys
    formOptions.optOne.Accelerator = strOPTION_ONE_ACCELERATOR
    formOptions.optTwo.Accelerator = strOPTION_TWO_ACCELERATOR
     
    ' Show Form and get user's choice
    fBalloonChoice = -1
    formOptions.Show
    
    ' Service choice
    On Error GoTo WizardError
    fnInitialBalloon (fBalloonChoice)
    Exit Sub
    
WizardError:
    ReportError Err
    
End Sub ' InitiateWizard


Public Sub ReportError(ErrorCode As ErrObject)
    Dim lErrorCode As Long
    Dim strErrorMsg As String
    
    '
    lErrorCode = ErrorCode.Number
    strErrorMsg = ErrorCode.Description
    
    ' Display error
    If lErrorCode Then DisplayErrorMsg strErrorMsg
    
    ' Always close the doc.
    ActiveDocument.Close wdDoNotSaveChanges
    
    ' Terminate Wizard
    End

End Sub ' ReportError


Public Sub DisplayErrorMsg(strErrMsg As String)
    
    MsgBox strErrMsg, vbExclamation + vbOKOnly + vbApplicationModal, strWIZ_NAME
    Err.Clear
    
End Sub


Public Sub ShowAssistantTip()

    On Error GoTo ErrorInitBalloon
    
    ' Create new Assistant Balloon
    Set objHelpTipBalloon = Assistant.NewBalloon
    With objHelpTipBalloon
        .Heading = strWIZ_NAME
        .Mode = msoModeModeless
        .Button = msoButtonSetNone
        .Text = strHelpTip(fBalloonChoice - 1)
    End With

    ' Show Balloon
    objHelpTipBalloon.Show
    Exit Sub
    
ErrorInitBalloon:
End Sub

' End Common Module
Attribute VB_Name = "Envelope"
' --------------------------------------------------------------------------------------
' WORD 97 MINI-WIZARD
' Envelope Wizard Specific Code
' --------------------------------------------------------------------------------------

Option Explicit

' --------------------------------------------------------------------------------------
' DECLARATIONS
' --------------------------------------------------------------------------------------

Public Const strWIZ_NAME = "Umschlag-Assistent"
Public Const iBALLOON_OPTIONS = 2 ' Number of items in InitialBalloon (First=1)
Public Const strBALLOON_HEADING = "Willkommen beim Umschlag-Assistenten."
Public Const strOPTION_ONE_ACCELERATOR = "E"   'This is the form accelerator for strInitialBalloon(0)
Public Const strOPTION_TWO_ACCELERATOR = "M"   'This is the form accelerator for strInitialBalloon(1)
Public strInitialBalloon(iBALLOON_OPTIONS) As String
Public strHelpTip(iBALLOON_OPTIONS) As String

Public Sub InitWizardStrings()
  
' NOTE: change constant  iBALLOON_OPTIONS if number of items in list changes
    ' Balloon / Form menu options
    strInitialBalloon(0) = "Einen Umschlag drucken."
    strInitialBalloon(1) = "Mehrere Umschl|fffd|ge mit der Seriendruck- Funktion von Word erstellen."

    ' Assistant help messages
strHelpTip(0) = "Geben Sie die Empf|fffd|nger- und Absenderadresse f|fffd|r den Umschlag ein, oder klicken Sie auf die Schaltfl|fffd|che ""Adre|fffd|buch"", um Namen aus einer Microsoft Outlook Kontaktliste oder einem Microsoft Exchange Pers|fffd|nlichen Adre|fffd|buch zu verwenden. Wenn Sie die Adresse eingegeben haben, klicken Sie auf ""Drucken""."
strHelpTip(1) = "Sie k|fffd|nnen die Seriendruckfunktion von Word verwenden, um Umschl|fffd|ge zu erstellen. Im oberen Teil des Seriendruck-Managers finden Sie weitere Anleitungen f|fffd|r die drei Schritte des Seriendruck-Managers."

End Sub  ' InitWizardStrings

Public Function fnInitialBalloon(iBtn)
    
    'On Error GoTo ErrorInitBalloon
    
    Select Case iBtn
        Case Is < 1 'Exit
            ActiveDocument.Close wdDoNotSaveChanges
        Case 1 ' Open the envelope dialog
            If fAssistantAvailable Then ShowAssistantTip
            Application.Dialogs(wdDialogToolsCreateEnvelope).Show
            If fAssistantAvailable Then objHelpTipBalloon.Close
        Case 2 ' Start a Mail Merge to envelopes
            ActiveDocument.MailMerge.MainDocumentType = wdEnvelopes
            If fAssistantAvailable Then ShowAssistantTip
            Application.Dialogs(wdDialogMailMergeHelper).Show
            If fAssistantAvailable Then objHelpTipBalloon.Close
    End Select
    Exit Function
    
End Function ' fnInitialBalloon

' End Envelope Wizard Module
Attribute VB_Name = "ThisDocument"
Attribute VB_Base = "0{00020906-0000-0000-C000-000000000046}"
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Private Sub Document_New()
    StartWizard
End Sub
Attribute VB_Name = "formOptions"
Attribute VB_Base = "0{54AF448A-4D63-11D0-B5E7-00AA00BBB76C}{54AF4477-4D63-11D0-B5E7-00AA00BBB76C}"
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = False






' Word Wizard
Option Explicit

Private Sub cmdCancel_Click()
    ' Close Form
    formOptions.Hide
End Sub

Private Sub cmdOK_Click()
    ' Save global choice
    If optOne.Value Then
        fBalloonChoice = 1
    Else
        fBalloonChoice = 2
    End If
    
    ' Close Form
    formOptions.Hide
End Sub

' InQuest injected base64 decoded content
' jZ N
' jZ 1

INQUEST-PP=macro
