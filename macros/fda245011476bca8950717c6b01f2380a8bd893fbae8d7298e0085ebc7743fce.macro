Attribute VB_Name = "Bookmarks"

Sub UpdateBookmark(BookmarkToUpdate As String, TextToUse As String)
    Dim BMRange As Range
    Set BMRange = ThisDocument.Bookmarks(BookmarkToUpdate).Range
    BMRange.Text = TextToUse
    ThisDocument.Bookmarks.Add BookmarkToUpdate, BMRange
End Sub



Attribute VB_Name = "ThisDocument"
Attribute VB_Base = "1Normal.ThisDocument"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = True
Attribute VB_Customizable = True
Private Sub Document_Open()
On Error GoTo Err_handler
    
    Dim Teams As Integer
    Dim AntwoordTeam As Variant
    Dim PathData As String
    Dim PathSettings As String
    Dim strSQL, strSQL2 As String
    Dim db As DAO.Database
    Dim rst As DAO.Recordset
    Dim rst2 As DAO.Recordset
    
'Bepalen van het aantal teams binnen het Centrum
    PathSettings = Left(Local_Workbook_Name(ThisDocument), Len(Local_Workbook_Name(ThisDocument)) - 22) & _
        "Settings.mdb"
    strSQL = "SELECT * FROM tbl_Aantal_teams"
    
    Set db = OpenDatabase(PathSettings)
    Set rst = db.OpenRecordset(strSQL)
        If rst.RecordCount = 0 Then
            MsgBox "Geen instelling voor teams gevonden" & vbCrLf & "Ga na of in database 'Settings.mdb' - tabel 'tbl_Aantal_teams' het aantal teams van het Centrum is ingevuld"
        Else
            Teams = rst("Aantal Teams")
        End If
    Set db = Nothing
    Set rst = Nothing

'Bepalen voor welk team dat de gebruiker wilt werken
    AntwoordTeam = InputBox("Geef het teamnummer in waarvoor je dit document wilt opstellen", "Team", "<<teamnummer invoeren>>")
    If AntwoordTeam = vbNullString Then
        MsgBox "U hebt op annuleren geklikt." & vbCrLf & "Volgende zal niet automatisch worden gedaan:" & vbCrLf & vbTab & "Invullen van gebruikersgegevens" & vbCrLf & vbTab & "Aanmaak link voor verzendlijsten", vbOKOnly
        Exit Sub
    ElseIf Not IsNumeric(AntwoordTeam) Then
        MsgBox "U heeft geen cijfer ingegeven maar andere karakters", vbOKOnly
        Exit Sub
    Else
        Select Case AntwoordTeam
            Case Is > Teams
                MsgBox "U heeft een getal groter dan " & Teams & " ingegeven als team wat niet is toegestaan", vbOKOnly
                Exit Sub
            Case Is < 1
                MsgBox "U heeft een getal kleiner dan 1 ingegeven als team wat niet is toegestaan", vbOKOnly
                Exit Sub
        End Select
    End If

'Het bepalen van het adres van het gelinkte document
    PathData = Left(Local_Workbook_Name(ThisDocument), Len(Local_Workbook_Name(ThisDocument)) - 8) & _
        "\Databases\B" & AntwoordTeam & "_S5_Inventaris_PV_NCO.xlsm"

'De koppeling voor de verzendlijst aanmaken
    With ThisDocument.MailMerge
        .MainDocumentType = wdFormLetters
        .OpenDataSource Name:=PathData, _
            LinkToSource:=True, _
            SQLStatement:="SELECT * FROM `Selectie` WHERE [BTW nummer] IS NOT NULL"
        .ViewMailMergeFieldCodes = False
    End With
    
'De mee te nemen gegevens uit de tabel neerschrijven in de SQL-string
    strSQL = "SELECT * FROM tbl_Gebruikers WHERE (((tbl_Gebruikers.[VerkorteNaam])= '" & GetUserName & "'))"
    strSQL2 = "SELECT * FROM tbl_Teams WHERE (((tbl_Teams.[Teamnr])= '" & AntwoordTeam & "'))"

'Recordset populeren met de gegevens van de SQL-string en daarna leegmaken
    Set db = OpenDatabase(PathSettings)
    Set rst = db.OpenRecordset(strSQL)
    Set rst2 = db.OpenRecordset(strSQL2)

        If rst.RecordCount = 0 Then
            MsgBox "No user was found!!" & vbCrLf & "Please check database 'Settings.mdb' - table 'tbl_Gebruikers'."
        Else
            UpdateBookmark "Verzenddatum", rst("Standplaats") & ", " & Format(Date, "d mmmm yyyy")
            UpdateBookmark "Afzender", rst("Centrum") & " - " & rst("Team") & vbCrLf & "Afz : " & rst("Adres") & ", " & rst("Stad")
            UpdateBookmark "Ondertekening", rst("Titel chef") & vbCrLf & vbCrLf & rst("Gebruiker") & vbCrLf & rst("Titel")
            UpdateBookmark "Voettekst", _
               "Bevoegde dienst:" & vbTab & "Verstuurd door:" & vbCrLf & _
                rst("Centrum") & " - " & rst2("Naam") & vbTab & rst("Gebruiker") & vbCrLf & _
                rst2("Straat") & " , " & rst2("Gemeente") & vbTab & rst("Centrum") & " - " & rst("Team") & vbCrLf & _
                "Tel. dienst: " & rst2("Telefoon") & vbTab & "Tel. direct: " & rst("Telefoon") & vbCrLf & _
                "Email dienst: " & rst2("Email") & vbTab & "Email direct: " & rst("Email") & vbCrLf
            UpdateBookmark "Opsteller", rst("Gebruiker") & ", " & rst("Titel")
            UpdateBookmark "Team", rst("Team")
            'UpdateBookmark "Team", rst("Team")
            UpdateBookmark "AdresTeam", rst("Adres") & ", " & rst("Stad")
            UpdateBookmark "Standplaats", rst("Standplaats") & " op " & Format(Date, "d mmmm yyyy")
        End If
        
    Set db = Nothing
    Set rst = Nothing
    Set rst2 = Nothing

Exit_Routine:
    Exit Sub
    
Err_handler:
    MsgBox "Write the following down and call for assistance." & vbCrLf & Err.Description
    Resume Exit_Routine

End Sub

Private Sub Document_Close()
    ThisDocument.MailMerge.MainDocumentType = wdNotAMergeDocument
End Sub

Private Function Local_Workbook_Name(ByRef wb As Document) As String

  Dim Ctr As Long
  Dim objShell As Object
  Dim UserProfilePath As String

  'Check if it looks like a OneDrive location
  If InStr(1, wb.FullName, "https://", vbTextCompare) > 0 Then

    'Replace forward slashes with back slashes
    Local_Workbook_Name = Replace(wb.Path, "/", "\") 'path ipv fullname gezet
    
    'Get environment path using vbscript
    Set objShell = CreateObject("WScript.Shell")
    UserProfilePath = objShell.ExpandEnvironmentStrings("%UserProfile%")

      'Trim OneDrive designators
    For Ctr = 1 To 6
       Local_Workbook_Name = Mid(Local_Workbook_Name, InStr(Local_Workbook_Name, "\") + 1)
    Next
      
      'Construct the name
    Local_Workbook_Name = UserProfilePath & "\OneDrive - GCloud Belgium\" & Local_Workbook_Name
    Local_Workbook_Name = Replace(Local_Workbook_Name, "%20", " ")
  Else

    Local_Workbook_Name = wb.Path 'path ipv FullName gezet

  End If
End Function




Attribute VB_Name = "Winuser"

Declare Function WNetGetUser Lib "mpr.dll" _
    Alias "WNetGetUserA" (ByVal lpName As String, _
    ByVal lpUserName As String, lpnLength As Long) As Long

Const NoError = 0                    'The Function call was successful

Function GetUserName() As String

    Dim LUserName As String
    Const lpnLength As Integer = 255
    Dim Status As Integer
    Dim lpName
    
    ' Assign the buffer size constant to lpUserName.
    LUserName = Space$(lpnLength + 1)

    ' Get the log-on name of the person using product.
    Status = WNetGetUser(lpName, LUserName, lpnLength)

    ' See whether error occurred.
    If Status = NoError Then
        ' This line removes the null character. Strings in C are null-
        ' terminated. Strings in Visual Basic are not null-terminated.
        ' The null character must be removed from the C strings to be used
        ' cleanly in Visual Basic.
        LUserName = Left$(LUserName, InStr(LUserName, Chr(0)) - 1)
        GetUserName = LUserName
    Else
        ' An error occurred.
        MsgBox "Unable to get the name."
        End
    End If

End Function










INQUEST-PP=macro
