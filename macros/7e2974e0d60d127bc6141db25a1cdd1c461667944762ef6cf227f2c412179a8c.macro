Attribute VB_Name = "Module1"
'--------------------------------------------------------------------------
' Module    : Module1
' Author    : Case (Ralf Stolzenburg)
' Date      : 17.04.2013
' Purpose   : WMIC - Beispiele Shell diverse Informationen ausgeben...
'--------------------------------------------------------------------------
Option Explicit
' Alle Internetexplorerfenster schliessen nach Name
Sub Main()
    Shell "wmic Process where ""name='iexplore.exe'"" call terminate", vbHide
End Sub
' Alle Internetexplorerfenster schliessen mit LIKE
Sub Main_1()
    Shell "wmic Process where ""name like '%iexplo%'"" call terminate", vbHide
End Sub
' Informationen |fffd|ber die angeschlossenen Laufwerke
Sub Main_2()
    ShellAndWait "wmic /output:clipboard logicaldisk get name,volumename,filesystem"
    Debug.Print fncCPRead()
End Sub
' Informationen |fffd|ber das Bios
Sub Main_3()
    ShellAndWait "wmic /output:clipboard bios list full"
    Debug.Print fncCPRead()
End Sub
' Bestimmte Informationen vom Bios (Hersteller und Seriennummer)
Sub Main_4()
    ShellAndWait "wmic /output:clipboard bios get manufacturer,serialnumber"
    Debug.Print fncCPRead()
End Sub
' Informationen |fffd|ber die angelegten Benutzerkonten
Sub Main_5()
    ShellAndWait "wmic /output:clipboard useraccount get /value"
    Debug.Print fncCPRead()
End Sub
' Informationen |fffd|ber laufende Anwendungen die NICHT im Windowspfad sind
Sub Main_6()
    ShellAndWait "wmic /output:clipboard PROCESS WHERE " & _
        """NOT ExecutablePath LIKE '%Windows%'"" GET ExecutablePath"
    Debug.Print fncCPRead()
End Sub
' Informationen |fffd|ber die Startvorg|fffd|nge
Sub Main_7()
    ShellAndWait "wmic /output:clipboard startup list full"
    Debug.Print fncCPRead()
End Sub
' Informationen |fffd|ber das Betriebssystem formatiert ausgeben
Sub Main_8()
    ShellAndWait "wmic /output:clipboard os get /value /format:htable"
    Call fncCPRead
    With Sheet1
        .Cells(1, 1).PasteSpecial
        .Cells.WrapText = False
        .Columns.EntireColumn.AutoFit
    End With
End Sub
' Informationen |fffd|ber die Prozesse formatiert ausgeben
Sub Main_9()
    ShellAndWait "wmic /output:clipboard process get /value /format:htable"
    Call fncCPRead
    With Sheet1
        .Cells(1, 1).PasteSpecial
        .Cells.WrapText = False
        .Columns.EntireColumn.AutoFit
    End With
End Sub
Public Sub Main_IE()
    ' NUR zum testen. Der IE wird viermal aufgerufen
    ' Dreimal mit meiner Blogseite und einmal mit einem Excelforum
    On Error GoTo Fin
    Call IE_Run("http://vbanet.blogspot.de/")
    Call IE_Run("http://vbanet.blogspot.de/")
    Call IE_Run("http://vbanet.blogspot.de/")
    Call IE_Run("http://www.herber.de/forum/")
Fin:
    If Err.Number <> 0 Then MsgBox "Error: " & _
        Err.Number & " " & Err.Description
End Sub
'--------------------------------------------------------------------------
' Module    : Module1
' Procedure : IE_Run
' Author    : Case (Ralf Stolzenburg)
' Date      : 17.04.2013
' Purpose   : Internetexplorer mit bestimmten Seiten starten...
'--------------------------------------------------------------------------
Private Sub IE_Run(ByVal strAddress As String)
    Dim objIEApp As Object
    Set objIEApp = CreateObject("InternetExplorer.Application")
    With objIEApp
        .Navigate strAddress
        Do: Loop Until .Busy = False
        Do: Loop Until .Busy = False
        .Visible = True
    End With
    Set objIEApp = Nothing
End Sub
'--------------------------------------------------------------------------
' Module    : Module1
' Procedure : fncCPRead
' Author    : Case (Ralf Stolzenburg)
' Date      : 17.04.2013
' Purpose   : Zwischenablage auslesen...
'--------------------------------------------------------------------------
Private Function fncCPRead() As String
    Dim objIEApp As Object
    Dim strClipB As String
    Set objIEApp = CreateObject("HTMLfile")
    strClipB = objIEApp.ParentWindow.ClipboardData.GetData("text")
    fncCPRead = strClipB
    Set objIEApp = Nothing
End Function
'--------------------------------------------------------------------------
' Module    : Module1
' Procedure : ShellAndWait
' Author    : Case (Ralf Stolzenburg)
' Date      : 17.04.2013
' Purpose   : ShellAndWait mit ausgeblendetem Dosfenster...
'--------------------------------------------------------------------------
Private Sub ShellAndWait(ByVal strPathName As String)
   Dim WshShell As Object
   On Error GoTo Fin
   Set WshShell = CreateObject("WScript.Shell")
   WshShell.Run strPathName, 0, True
Fin:
   Set WshShell = Nothing
   If Err.Number <> 0 Then MsgBox "Error: " & _
       Err.Number & " " & Err.Description
End Sub
Attribute VB_Name = "Sheet1"
Attribute VB_Base = "0{00020820-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Option Explicit

Attribute VB_Name = "ThisWorkbook"
Attribute VB_Base = "0{00020819-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Option Explicit
Private Sub Workbook_Open()
    Call MsgBox("Codes im VBA-Editor starten bzw. testen!" _
        & vbCrLf & "Start and test code in the VBA editor!" _
        , vbInformation, "Codes testen!")
    Application.VBE.MainWindow.Visible = True
End Sub


INQUEST-PP=macro
