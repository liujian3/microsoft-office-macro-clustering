Attribute VB_Name = "Foglio1"
Attribute VB_Base = "0{00020820-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Attribute VB_Name = "Foglio2"
Attribute VB_Base = "0{00020820-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Attribute VB_Name = "Foglio3"
Attribute VB_Base = "0{00020820-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Attribute VB_Name = "Foglio5"
Attribute VB_Base = "0{00020820-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Attribute VB_Name = "Foglio6"
Attribute VB_Base = "0{00020820-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True
Attribute VB_Name = "Modulo1"
Option Explicit

Public Sub Costi_Cdc()

'---------------------------------------
Dim Data As Integer 'Anno: variabile _
    richiesta all 'utente
Dim RowBO As Integer 'BO = Riga Budget _
    Owner principale
Dim RowDR As Integer 'DR = Riga _
    Direct Report
Dim Rep1 As Integer 'Rep1 = Colonna _
    Budget Owner Principale
Dim Rep2 As Integer 'Rep2 = Colonna _
    Direct Report
Dim Ciclo As Variant 'Contiene _
    massima riga BO e DR
Dim Rep As Variant 'Nome Rep1 e Rep2
Dim Cat2 As Integer 'Cat2 = Colonna _
    Categoria di costo 2
Dim Actual As Integer 'Colonna Actual
Dim Budget As Integer 'Colonna Budget
Dim Path As String 'percorso directory
Dim I As Integer 'variabile iterativa
Dim J As Integer 'variabile iterativa
Dim Nome As String 'Nome del _
    responsabile (iterativa)
Dim Psw As String 'Password della _
    cartella (iterativa)
Dim ColonnaNome As Variant 'Colonna del _
    nome responsabili
Dim ColonnaPsw As Variant 'Colonna della _
    Password del singolo
Dim UltimaRiga As Long 'Ultima riga. _
    Valore potenzialmente alto
'---------------------------------------

Dim wbIn As Excel.Workbook
Set wbIn = ThisWorkbook

Dim wshInDb As Excel.Worksheet
Set wshInDb = wbIn.Sheets("db")

Dim wshInDati As Excel.Worksheet
Set wshInDati = wbIn.Sheets("dati")

Application.DisplayAlerts = False
Application.ScreenUpdating = False

Application.SheetsInNewWorkbook = 1

Data = InputBox("Inserire l'anno di " & _
    "analisi (p.es. <2005>)", _
    "Inserire anno")

Path = "C:\Expenses Report\"

'scopro la riga finale dei Budget OWNER
RowBO = wshInDb.Range("I1").End(xlDown).Row

'scopro la riga finale dei riporti
RowDR = wshInDb.Range("K1").End(xlDown).Row

'scopro che posizione ha il campo dell'_
'indirizzo del budget owner che si
'chiama Report1
Rep1 = wshInDati.Range("1:1"). _
    Find(what:="Report1").Column

'scopro che posizione ha il campo dell'_
'indirizzo del budget owner che si
'chiama Report2
Rep2 = wshInDati.Range("1:1"). _
    Find(what:="Report2").Column

'scopro che posizione ha il campo della
'categoria di costo che si chiama Cat2
Cat2 = wshInDati.Range("1:1"). _
    Find(what:="Cat2").Column

'Scopro la posizione del campo Actual
Actual = wshInDati.Range("1:1"). _
    Find(what:="ACTUAL").Column

'Scopro la posizione del campo Budget
Budget = wshInDati.Range("1:1"). _
    Find(what:="Budget").Column

'---------------------------------
'Caricamento variabili vettore

Ciclo = Array(RowBO, RowDR)
Rep = Array(Rep1, Rep2)
ColonnaNome = Array("I", "K")
ColonnaPsw = Array("J", "L")
'---------------------------------

Dim wbOut       As Excel.Workbook
Dim wshOutDb    As Excel.Worksheet
Dim wshOutPivot As Excel.Worksheet

'inizia il ciclo principale
For J = 0 To 1

    'inizia il ciclo secondario
    For I = 2 To Ciclo(J)
        
        Nome = wshInDb. _
            Range(ColonnaNome(J) & I)
        
        Psw = wshInDb. _
            Range(ColonnaPsw(J) & I)
        
        wshInDati.Range("A1"). _
            AutoFilter Field:= _
            Rep(J), Criteria1:=Nome
        
        wshInDati.Range("1:" & _
            wshInDati.Range("A1"). _
            End(xlDown).Row).Copy
        
        Set wbOut = Workbooks.Add
        Set wshOutDb = wbOut.Sheets(1)
        
        wshOutDb.Range("A1"). _
            PasteSpecial _
            Paste:=xlPasteValues
                
     'Elimina importi compresi tra _
      1 e -1 per l'actual ed elimina _
      gli eventuali importi a zero _
      per il Budget
    
        wshOutDb.Range("A1").AutoFilter _
            Field:=Actual, Criteria1:= _
            ">=-1", Operator:=xlAnd, _
            Criteria2:="<=1"
          
        UltimaRiga = wshOutDb. _
            Range("A1").End(xlDown).Row
        
        wshOutDb.Range("2:" & _
            UltimaRiga).Delete
        
        wshOutDb.Range("A1").AutoFilter
        
        wshOutDb.Range("A1").AutoFilter _
            Field:=Budget, Criteria1:="=0"
        
        UltimaRiga = wshOutDb.Range("A1"). _
            End(xlDown).Row
        
        wshOutDb.Range("2:" & UltimaRiga). _
            Delete
        
        wshOutDb.Range("A1").AutoFilter
        
        PIVOT
        
        wbOut.SaveAs Filename:=Path _
            & Data _
            & " - " & Nome _
            & " - Report.xls" _
          , Password:=Psw ' excel 2007 _
          , FileFormat:=xlOpenXMLWorkbook '

        wbOut.Close
        wshInDati.Range("a1").AutoFilter
        
    Next I

Next J

Application.DisplayAlerts = True
Application.ScreenUpdating = True

End Sub
Private Sub PIVOT()

Dim UltimaRiga    As Long
Dim DataMax       As Long
Dim DataMin       As Long
Dim groupRange    As Range

Dim wbOut    As Excel.Workbook
Dim wshOutData  As Excel.Worksheet
Dim wshOutPivot As Excel.Worksheet

Dim objPCch As Excel.PivotCache
Dim objPTbl As Excel.PivotTable
    
    Set wbOut = Application.ActiveWorkbook
    Set wshOutData = Application.ActiveSheet
    Set wshOutPivot = wbOut.Worksheets.Add
    
    UltimaRiga = wshOutData.Range("A1"). _
        End(xlDown).Row
    
    DataMax = Application. _
        WorksheetFunction.Max _
        (wshOutData.Range("C:C"))
    
    DataMin = Application. _
        WorksheetFunction.Min _
        (wshOutData.Range("C:C"))
    
    Set objPCch = wbOut.PivotCaches. _
        Add(xlDatabase, SourceData:= _
        wshOutData.Range(wshOutData.Cells(1, 1) _
      , wshOutData.Cells(UltimaRiga, 20)))
        
    Set objPTbl = objPCch.CreatePivotTable _
        (wshOutPivot.Range("A1") _
      , TableName:="Tabella_pivot1" _
      , DefaultVersion:=xlPivotTableVersion10)
      
'imposta il formato TabellaPivot come "tabella"
'valido per Excel 2007
'    objPTbl.RowAxisLayout xlTabularRow

    objPTbl.AddFields RowFields:= _
        Array("Cat2", "Cat1", "Natura" _
      , "DATE"), ColumnFields:="Owner2" _
      , PageFields:=Array("BU", "Prod", "PJC")

    objPTbl.PivotFields("Cat1").Subtotals _
      = Array(False, False, False _
            , False, False, False _
            , False, False, False _
            , False, False, False)
            
    With objPTbl.PivotFields("Budget")
        .Orientation = xlDataField
        .Caption = "BDGT"
        .Function = xlSum
        .NumberFormat = "#,##0;[Red](#,##0)"
    End With
    
    With objPTbl.PivotFields("ACTUAL")
        .Orientation = xlDataField
        .Caption = "ACTs"
        .Function = xlSum
        .NumberFormat = "#,##0;[Red](#,##0)"
    End With

    objPTbl.DataPivotField.Orientation _
        = xlColumnField
    
'formatta il campo data nel foglio 1
    wshOutData.Columns("C:C").NumberFormat = _
    "dd/mm/yy;@"
    objPTbl.PivotCache.Refresh

'raggruppa per Trimestre e Mese il campo Date
    Set groupRange = objPTbl.PivotFields("DATE") _
        .DataRange
        
        groupRange.Cells(1).Group _
        Start:=DataMin, End:=DataMax, _
        Periods:=Array(False, False, False, _
        False, True, True, False)

    With objPTbl.PivotFields("Trimestri")
        .Orientation = xlPageField
        .Caption = "Quarter"
    End With

    With objPTbl.PivotFields("DATE")
        .Orientation = xlPageField
        .Caption = "Month"
    End With
    
'aggiunge campi calcolati
    objPTbl.CalculatedFields.Add "DELTA" _
      , "=Budget-ACTUAL" _
      , True
    objPTbl.CalculatedFields.Add "DELTA%" _
      , "=IF(Budget=0,0,DELTA/Budget)" _
      , True
    objPTbl.PivotFields("DELTA"). _
        Orientation = xlDataField
    objPTbl.PivotFields("DELTA%"). _
        Orientation = xlDataField
        
'formatta la varianza percentuale come percentuale
    With objPTbl.PivotFields("Somma di DELTA%")
        .NumberFormat = "0%;[Red](0%)"
    End With

'rinomina i delta
    objPTbl.DataPivotField.PivotItems _
        ("Somma di DELTA").Caption = "Variance"
    objPTbl.DataPivotField.PivotItems _
        ("Somma di DELTA%").Caption = "Variance%"
    
    ActiveWindow.Zoom = 80
    
Imp_Stampa
    
    wshOutData.Visible = False
    Cells.EntireColumn.AutoFit

End Sub


Private Sub Imp_Stampa()
    With ActiveSheet.PageSetup
        .Orientation = xlLandscape
        .Zoom = False
        .FitToPagesWide = 1
        .FitToPagesTall = 1
    End With
End Sub


Attribute VB_Name = "ThisWorkbook"
Attribute VB_Base = "0{00020819-0000-0000-C000-000000000046}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = True


INQUEST-PP=macro
